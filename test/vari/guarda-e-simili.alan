-- Alan >= 3.0beta6 build 1866 | Alan Italian >= v0.13.0-Alpha
--==============================================================================
-- "Guarda E Verbi Affini" by Tristano Ajmone, 2018.
--==============================================================================
OPTIONS WIDTH 81.

IMPORT 'libreria.i'.   -- ALAN Libreria Standard Italiana v2.1
IMPORT '../inc_debaca.i'. -- Comandi per il debugging


THE mia_AT IsA blocco_definizioni
  ------------------------------------------------------------------------------
  HAS       titolo  "Guarda E Verbi Affini".
  HAS  sottotitolo  "Avventure per testare il gruppo di verbi ""guarda"".".
  HAS       autore  "Tristano Ajmone".
  HAS         anno   2018.
  HAS     versione  "1".
  HAS        AlanV  "v3.0beta6 build 1866".
  ------------------------------------------------------------------------------
END THE.

-- =============================================================================

-- La Cripta

-- =============================================================================
THE cripta IsA stanza.
  NAME 'Nella Cripta'
  DESCRIPTION
    "Ogni cosa nella cripta è avvolta nella soffusa luce lunare che filtra dal
     lucernario."
  HAS desc_pareti
    "Gli affreschi sulle pareti raffigurano l'apocalisse.".
END THE cripta.
--------------------------------------------------------------------------------
-- Gli Affreschi
--------------------------------------------------------------------------------
-- Per implementare gli affreschi sfruttiamo le pareti predefinite nella classe
-- "stanza".

SYNONYMS affreschi = pareti.

ADD TO EVERY stanza
  VERB esamina
    CHECK ogg <> pareti
      ELSE SAY THIS:desc_pareti.
  END VERB.
END ADD.
--==============================================================================
-- Il Lucernario
--==============================================================================
THE lucernario IsA object AT cripta.
  IS
    distante.
    NOT raggiungibile.
  HAS xDesc
    "È un lucernario rotondo, con dei raggi che ricordano una rosa o un disco
    solare. Si trova molto in alto.".
END THE lucernario.
--==============================================================================
-- Le Bare
--==============================================================================
EVERY bara IsA object.
  OPAQUE CONTAINER.
  NAME bara.
  NAME bare.
  HAS articolo "la".
  IS NOT prendibile.
  HAS xDesc "La fioca luce lunare non ti consente di vedere cosa ci sia dentro.".      
END EVERY bara.
--------------------------------------------------------------------------------
-- Bara Rossa
--------------------------------------------------------------------------------
THE bara_rossa IsA bara AT cripta.
  NAME bara rossa.
END THE bara_rossa.
--------------------------------------------------------------------------------
-- Bara Nera
--------------------------------------------------------------------------------
THE bara_nera IsA bara AT cripta.
  NAME bara nera.

  VERB guarda_in
    DOES AFTER
      MAKE THIS NOT OPAQUE.
  END VERB.

  VERB esamina
    DOES AFTER
      "Ma sai già che contiene uno scheletro."
  END VERB.
END THE bara_nera.

THE scheletro IsA object IN bara_nera.
  HAS articolo "lo".
  HAS xDesc
    "Lo scheletro giace in una postura pacifica sfoggiando in eterno il vestito
     del suo funerale. Il tempo, che ne ha divorato avidamente la carne, sembra
     aver risparmiato i suoi capelli, lasciando al buio il compito di tingerli
     di cenere. Fitte ragnatele coprono il suo viso. Gli scheletri hanno un viso,
     giusto?".
END THE scheletro.
--==============================================================================
-- Lo Scricchiolio
--==============================================================================
THE scricchiolìo IsA suono AT cripta.
  NAME scricchiolìo.
  NAME scricchiolio.
  HAS articolo "lo".
  DESCRIPTION "Odi un inquietante scricchiolìo in sottofondo."
END THE scricchiolìo.
--==============================================================================
-- La Pianta
--==============================================================================
-- Mettiamo anche un oggetto scenico ("scenario") da testare.
THE pianta IsA object AT cripta.
  HAS articolo "la".
  IS scenario.
  HAS xDesc "È una sorta di pianta funerea.".
END THE pianta.
--==============================================================================
-- Il Cubo
--==============================================================================
THE cubo IsA object AT cripta.
  HAS xDesc "Un misterioso cubo nero.".
END THE cubo.

-- =============================================================================

-- ATTORI PNG

-- =============================================================================
-- Popoliamo la scena con qualche PNG per testare le risposte.

--==============================================================================
-- Il Fantasma
--==============================================================================
-- Mettiamo un attore maschio senza nome proprio...
THE fantasma IsA maschio AT cripta.
  HAS xDesc
    "Un vero fantasma in carne ed ossa -- Ops! mi correggo, ""in spirito e 
     plasma""!".
END THE fantasma.

--------------------------------------------------------------------------------
Start at cripta.
DESCRIBE intestazione.

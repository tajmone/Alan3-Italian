--==============================================================================
-- "Provini Attori" by Tristano Ajmone, 2018.
--==============================================================================
-- Questa avventura serve a testare la corretta implementazione degli attori
-- nella Libreria, in particolar modo l'inizializzazione dei loro attributi per
-- la lingua italiana.
--------------------------------------------------------------------------------
IMPORT 'libreria.i'. -- ALAN Libreria Standard Italiana v2.1


THE mia_AT IsA blocco_definizioni
  ------------------------------------------------------------------------------
  HAS       titolo  "Provini Attori".
  HAS  sottotitolo  "Set cinematografici per effettuare test sugli attori.".
  HAS       autore  "Tristano Ajmone".
  HAS         anno   2018.
  HAS     versione  "1".
  ------------------------------------------------------------------------------
END THE.

THE stanza0 IsA STANZA.
  NAME 'Atrio Principale'.
  DESCRIPTION
    "A NORDEST c'è una stanza piena di PNG con nomi propri.$n"
    "A NORDOVEST c'è una stanza piena di PNG senza nomi propri."
  EXIT NORDEST TO stanza1.
  EXIT NORDOVEST TO stanza2.
END THE.

--==============================================================================
-- Debug degli Attributi degli ACTOR
--==============================================================================
-- Il verbo 'debaca' mostra gli attributi per la grammatica italiana (usati
-- dalla libreria) di un'istanza di attore. Utile per verificarne la corretta 
-- inizializzazione da parte della libreria.
-- 
-- NOTA: "debaca" è la forma italianizzata del verbo inglese "debug" ("rimuovere
--       un baco"). Se ritieni che il termine sia orribile, o se questo offende
--       la tua sensibilità linguistica, sei libero di presentare un reclamo
--       ufficiale all'ADGAT:
-- 
--            ADGAT - Associazione Difesa Giocatori di Avventure Testuali
--            Piazza della Repubblica d'IF, 41/bis
--            10100 -- Torino
--------------------------------------------------------------------------------
SYNTAX debaca = debaca (act)*
  WHERE act IsA ACTOR
    ELSE "$+1 non è un attore!"

ADD TO EVERY ACTOR
  VERB debaca
    DOES
      "$n+------------------------------------------"
      "$n| NOME: $1"
      "$n+------------------------------------------"
      "$n| HA NOME PROPRIO:$$"
      IF THIS IS named
        THEN " Sì"
        ELSE " No"
      END IF.
      "$n| GENERE:$$"
      IF THIS IS femminile
        THEN " femmina"
        ELSE " maschio"
      END IF.
      "$n| NUMERO:$$"
      IF THIS IS plurale
        THEN " plurale"
        ELSE " singolare"
      END IF.
      "$n| ARTICOLO: " SAY act:articolo.
      "$n| FORMA DETERMINATA:$t$+1"
      "$n| FORMA INDETERMINATA:$t$01"
      "$n| VOCALE: " SAY act:vocale.
      "$n| PREP. DI:$t" SAY act:prep_DI.
      "$n| PREP.  A:$t" SAY act:prep_A.
      "$n| PREP. DA:$t" SAY act:prep_DA.
      "$n| PREP. IN:$t" SAY act:prep_IN.
      "$n| PREP. SU:$t" SAY act:prep_SU.
      "$n+------------------------------------------"
  END VERB debaca.
END ADD TO.

--==============================================================================
-- PNG Con Nomi Propri
--==============================================================================
-- Gli attori con nome proprio sono un caso speciale per quanto riguarda gli
-- attributi per l'italiano: la libreria deve occuparsi di inizializzarli una
-- seconda volta (dopo l'avvenuta inizializzazione sulla classe THING) poiché
-- la presenza del nome proprio richiede che vengano modificate le preposizioni
-- da articolate a semplici, affinché nei messaggi si legga:
--
--    "Hai dato la palla A Marta."
--
-- e non: 
--
--    "Hai dato la palla ALLA Marta." 
--------------------------------------------------------------------------------

THE stanza1 IsA STANZA.
  NAME 'Sala dei Nomi Propri'.
  DESCRIPTION "In questo set li attori hanno nomi propri.$n"
              "A OVEST c'è l'altro set.$p"
  EXIT OVEST to stanza2.
END THE.

-- ==================
-- Maschile Singolare
-- ==================
THE Mario IsA maschio AT stanza1.
  HAS named.
END THE.
-- ===================
-- Femminile Singolare
-- ===================
THE Maria IsA femmina AT stanza1.
  HAS named.
END THE.
-- ================
-- Maschile Plurale
-- ================
-- Questo esempio è più ipotetico che pratico; comunque vale la regola che se
-- diversi attori con nome proprio vengono rappresentati tramite un'unica
-- istanza, se anche solo uno di essi è maschile ci si dovrà riferire ad essi
-- con il maschile.
THE Romeo_Giulietta IsA maschio AT stanza1.
  HAS named.
  IS plurale.
  NAME 'Romeo e Giuglietta'.
  NAME Romeo.
  NAME Giuglietta.
END THE.

-- =================
-- Femminile Plurale
-- =================
THE Thelma_Louise IsA femmina AT stanza1.
  HAS named.
  IS plurale.
  NAME 'Thelma e Louise'.
  NAME Thelma.
  NAME Louise.
END THE.

--==============================================================================
-- PNG Senza Nomi Propri
--==============================================================================
THE stanza2 IsA STANZA.
  NAME 'Set Attori in Costume'.
  DESCRIPTION "In questo set gli attori indossano costumi di personaggi, e non
               hanno nomi propri.$n"
              "A EST c'è l'altro set.$p"
  EXIT EST to stanza1.
END THE.


-- ============
-- Articolo: IL
-- ============
THE vampiro IsA maschio AT stanza2.
  HAS articolo "il".
END THE.
-- ============
-- Articolo: LO
-- ============
THE zombie IsA maschio AT stanza2.
  HAS articolo "lo".
END THE.
-- ================
-- Articolo: L' (m)
-- ================
THE orco IsA maschio AT stanza2.
  HAS articolo "l'".
  NAME orco.
  NAME 'l''orco'.
END THE.
-- ============
-- Articolo: LA
-- ============
THE strega IsA femmina AT stanza2.
  HAS articolo "la".
END THE.
-- ================
-- Articolo: L' (f)
-- ================
THE arpia IsA femmina AT stanza2.
  HAS articolo "l'".
  IS femmninile.
END THE.
-- ===========
-- Articolo: I
-- ===========
THE gemelli IsA maschio AT stanza2.
  HAS articolo "i".
  NAME 'gemelli siamesi'.
  NAME 'gemelli'.
END THE.
-- =============
-- Articolo: GLI
-- =============
THE sciamani IsA maschio AT stanza2.
  HAS articolo "gli".
END THE.
-- ============
-- Articolo: LE
-- ============
THE fate IsA femmina AT stanza2.
  HAS articolo "le".
END THE.

--------------------------------------------------------------------------------
Start at stanza0.
DESCRIBE intestazione.

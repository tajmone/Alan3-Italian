--+============================================================================+
--| * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  |
--|  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * |
--| * * * +------------------------------------------------------------+  * *  |
--|  * *  |                                                            | * * * |
--| * * * |         E M P O R I O   G I O R G I O   A L A N I          |  * *  |
--|  * *  |                                                            | * * * |
--| * * * +------------------------------------------------------------+  * *  |
--|  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * |
--| * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  |
--+============================================================================+
-->rev_info
--| :revdate:     2019-02-20
--| :revnumber:   0.0.18-Alpha
--| :revremark:   Alan 3.0beta6 build 1866
--<<
-->-----------------------------------------------------------------------------
--| *Emporio Giorgio Alani* by Tristano Ajmone, 2018.
--| Un'avventura per testare le funzionalità del vestiario di Alan-Italian.
--<-----------------------------------------------------------------------------
OPTIONS WIDTH 81.

IMPORT 'libreria.i'.      -- ALAN Libreria Standard Italiana v2.1
IMPORT '../inc_debaca.i'. -- Comandi per il debugging


THE mia_AT IsA blocco_definizioni
  ------------------------------------------------------------------------------
  HAS       titolo  "Emporio Giorgio Alani".
  HAS  sottotitolo  "Un set per testare il vestiario.".
  HAS       autore  "Tristano Ajmone".
  HAS         anno   2019.
  HAS     versione  "1".
  HAS        AlanV  "v3.0beta6 build 1870".
  ------------------------------------------------------------------------------
END THE.

-->
--~=============================================================================
--| == Personalizzazione classe `location`
--~=============================================================================

--| Aggiungiamo a tutti i luoghi alcuni attributi personalizzati:

--| 1. Un attributo stringa per definire un nome alternativo per la stanza, da
--|    mostrare quando la si menziona nei messaggi. Di solito, si tratta di una
--|    versione più breve del nome originale, tutto in minuscolo. Attualmente è
--|    utilizzato dall'EVENTo `Riattiva_Assistente`, per menzionare da quale
--|    stanza è arrivata (se la stringa è vuota, verrà usato `LOCATION OF`).
--|
--| 2. Un attributo stringa per immagazzinare la lista delle uscite di una stanza.
--|    Attualmente utilizzato dal verbo `mostra_uscite`.
--|
--| [NOTE]
--| ============================================================================
--| In questa avventura definiremo anche l'articolo in quelle stanze cui verrà
--| fatto riferimento nei messaggi di `Riattiva_Assistente`, così sarà possibile
--| recuperare le preposizioni articolate idonee al nome della stanza tramite
--| `prep_DA`, ecc. (es. "`dall'atrio`").
--| ============================================================================

ADD TO EVERY location
  HAS menzione_stanza "".   -- (opzionale) nome alternativo nei messaggi.
  HAS uscite_stanza   "".   -- (opzionale) elenco uscite della stanza.
END ADD TO location.

--~=============================================================================
--~* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~-----------------------------------------------------------------------------
--|
--| == MAPPA DEL GIOCO
--|
--~-----------------------------------------------------------------------------
--~* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~=============================================================================
--| 
--| Iniziamo ad abbozzare la mappa del gioco, ci occuperemo dell'arredamento più
--| avanti, per rendere più leggibile il codice.
--| 
--~
--~
--~         +-------+-------+   +-------+-------+   +-------+-------+
--~         |  REP. LAVORO  |---|      ???      |---|      ???      |
--~         +-------+-------+   +-------+-------+   +-------+-------+
--~                 |        \          |          /        |
--~                 |         \         |         /         |
--~                 |          \        |        /          |
--~         +-------+-------+   +-------+-------+   +-------+-------+
--~         |  REP. SPORT   |---|   CAMERINO    |---| REP. COSTUMI  |
--~         +-------+-------+   +-------+-------+   +-------+-------+
--~                 |          /        |        \          |
--~                 |         /         |         \         |
--~                 |        /          |          \        |
--~         +-------+-------+   +-------+-------+   +-------+-------+
--~         |  REP. DONNA   |---|     ATRIO     |---|   REP. UOMO   |
--~         +---------------+   +---------------+   +---------------+
--~
--| image::ega.png[align="center"]

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Entrata Emporio GA
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_entrata IsA luogo_esterno.
  NAME 'Davanti all''Emporio Alani'.
  EXIT nord TO ega_atrio
    DOES
      --------------------
      -- Porte Automatiche
      --------------------
      SAY ega_porte:testo_attraversa.
      LOCATE ega_porte AT ega_atrio.
      LOCATE ega_telaio_porte AT ega_atrio.
      -----------------------
      -- Assistente Personale
      -----------------------
      MAKE assistente_ega seguendo.
  END EXIT.
  DESCRIPTION "Ti trovi di fronte all'entrata dell'Emporio Giorgio Alani."
  HAS uscite_stanza "NORD per entrare nell'Emprio.".
END THE ega_entrata.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Atrio dell'Emporio
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_atrio IsA stanza.
  NAME 'Atrio dell''Emporio Alani'.
  HAS menzione_stanza "atrio".
  HAS articolo "l'".   --> Per una corretta inizializzazione delle preposizioni!
  EXIT est   TO ega_uomo.
  EXIT ovest TO ega_donna.
  EXIT nord  TO ega_camerino.
  EXIT sud   TO ega_entrata
    DOES
      --------------------
      -- Porte Automatiche
      --------------------
      SAY ega_porte:testo_attraversa.
      LOCATE ega_porte AT ega_atrio.
      LOCATE ega_telaio_porte AT ega_atrio.
      -----------------------
      -- Assistente Personale
      -----------------------
      MAKE assistente_ega NOT seguendo.
      "L'assistente ti saluta mentre lasci l'emporio."
  END EXIT.
  DESCRIPTION
    "Questo lussuoso atrio conduce ai vari reparti dell'emporio."
  HAS uscite_stanza
    "EST al reparato uomo, OVEST al reparto donna, e NORD al camerino.".
END THE ega_atrio.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Camerino
--~-----------------------------------------------------------------------------
--~=============================================================================
--| Collochiamo il camerino al centro dell'emprio (che è quadrato) di modo che
--| sia accessibile da tutti i reparti.

THE ega_camerino IsA stanza.
  NAME 'Camerino'.
  EXIT sud TO ega_atrio
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT sudest TO ega_uomo
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT est TO ega_costumi
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT nordest TO ega_vacante2
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT nord TO ega_vacante1
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT nordovest TO ega_lavoro
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT ovest TO ega_sport
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  EXIT sudovest TO ega_donna
    DOES SCHEDULE Riattiva_Assistente AT hero AFTER 0.
  END EXIT.
  DESCRIPTION
    "Le tende scorrevoli che avvolgono il camerino ti concedono la privacy per
     poterti spogliare e provare i vestiti."
  HAS uscite_stanza
    "NORD al reparto 1, NORDEST al reparto 2,
     EST ai costumi,
     SUDEST al reparto uomo,
     SUD all'atrio,
     SUDOVEST al reparto donna,
     OVEST al reparto sport, e
     NORDOVEST al reparto lavoro.".
  VERB spogliati -- Consenti all'eroe di spogliarsi all'interno del camerino:
    DOES ONLY
      IF COUNT IsA indumento, DIRECTLY IN hero, IS indossato > 0
        THEN
          "Ti spogli di tutti gli indumenti che indossavi e li getti a terra."
          FOR EACH ind_indossato IsA indumento, DIRECTLY IN Hero, IS indossato
            DO
              MAKE ind_indossato NOT indossato.
              LOCATE ind_indossato AT CURRENT LOCATION.
          END FOR.
        ELSE "Non stai indossando nulla.."
      END IF. 
  END VERB spogliati.  
END THE ega_camerino.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto Abbigliamento Uomo
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_uomo IsA stanza.
  NAME 'Reparto Uomo'.
  HAS menzione_stanza "reparto uomo".
  EXIT ovest     TO ega_atrio.
  EXIT nord      TO ega_costumi.
  EXIT nordovest TO ega_camerino.
  DESCRIPTION
    "Qui puoi trovare tutto ciò che un uomo desidererebbe indossare."
  HAS uscite_stanza
    "NORD ai Costumi, NORDOVEST al camerino, e OVEST all'atrio.".
  END THE ega_uomo.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto Abbigliamento Donna
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_donna IsA stanza.
  NAME 'Reparto Donna'.
  HAS menzione_stanza "reparto donna".
  EXIT est     TO ega_atrio.
  EXIT nord    TO ega_sport.
  EXIT nordest TO ega_camerino.
  DESCRIPTION
    "Non vi è sogno di moda femminile che i vestiti di questo reparto non
     possano esaudire."
  HAS uscite_stanza
    "NORD al reparto sport, NORDEST al camerino, e EST all'atrio.".
END THE ega_donna.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto costumi
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_costumi IsA stanza.
  NAME 'Reparto costumi'.
  HAS menzione_stanza "reparto costumi".
  EXIT sud   TO ega_uomo.
  EXIT ovest TO ega_camerino.
  EXIT nord  TO ega_vacante2.
  DESCRIPTION
    "Qui puoi trovare un custome per ogni occasione."
  HAS uscite_stanza
    "SUD al reparto uomo, NORD al reparto 2, e OVEST al camerino.".
END THE ega_costumi.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto Abbigliamento Sportivo
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_sport IsA stanza.
  NAME 'Reparto Abbigliamento Sportivo'.
  HAS menzione_stanza "reparto sportivo".
  EXIT nord TO ega_lavoro.
  EXIT sud  TO ega_donna.
  EXIT est  TO ega_camerino.
  DESCRIPTION
    "Attorno a te vedi abbigliamento e attrezzatura per più sport di quanti tu
     conosca."
  HAS uscite_stanza
    "NORD al reparto lavoro, SUD al reparto donna, e EST al camerino.".
END THE ega_sport.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto Abbigliamento Lavoro e Antinfortunistica
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_lavoro IsA stanza.
  NAME 'Reparto Abbigliamento Lavoro e Antinfortunistica'.
  HAS menzione_stanza "reparto lavoro".
  EXIT sud    TO ega_sport.
  EXIT est    TO ega_vacante1.
  EXIT sudest TO ega_camerino.
  DESCRIPTION
    "Qui puoi trovare uniformi e accessori antinfortunistici per varie professioni."
  HAS uscite_stanza
    "SUD al reparto sport, EST al reparto 1, e SUDEST al camerino.".
END THE ega_lavoro.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto 1 (non assegnato)
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_vacante1 IsA stanza.
  NAME 'Reparto #1 (vuoto)'.
  HAS menzione_stanza "reparto 1".
  EXIT sud   TO ega_camerino.
  EXIT est   TO ega_vacante2.
  EXIT ovest TO ega_lavoro.
  DESCRIPTION
    "Questo reparto non è ancora stato assegnato; i suoi scaffali sono vuoti."
  HAS uscite_stanza
    "EST al reparto 2, OVEST al reparto lavoro, e SUD al camerino.".
END THE ega_vacante1.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Reparto 2 (non assegnato)
--~-----------------------------------------------------------------------------
--~=============================================================================

THE ega_vacante2 IsA stanza.
  NAME 'Reparto #2 (vuoto)'.
  HAS menzione_stanza "reparto 2".
  EXIT sudovest TO ega_camerino.
  EXIT ovest    TO ega_vacante1.
  EXIT sud      TO ega_costumi.
  DESCRIPTION
    "Questo reparto non è ancora stato assegnato; i suoi scaffali sono vuoti."
  HAS uscite_stanza
    "SUD ai costumi, OVEST al reparto 1, e SUDOVEST al camerino.".
END THE ega_vacante2.

--~=============================================================================
--~* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~-----------------------------------------------------------------------------
--|
--| == Il vestiario
--|
--~-----------------------------------------------------------------------------
--~* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--~=============================================================================


--~=============================================================================
--~-----------------------------------------------------------------------------
--| === Assegnazione degli strati d'indossamento
--~-----------------------------------------------------------------------------
--~=============================================================================

--| Il sistema stratificato della Libreria consente di mappare liberamente gli
--| strati di indossamento delle varie zone del corpo ai vari tipi di vestiario.

--| L'assegnazione di specifici valori di strato (nelle zone di indossamento) a
--| determinate categorie di indumenti è una scelta arbitraria, perlopiù dettata
--| dal tipo di vestiario impiegato nell'avventura. Nel nostro caso, avendo a
--| che fare con un'avventura completamente incentrata sul vestiario, dovremo
--| pianificare nel dettaglio la mappatura dei valori di indossamento dei vari
--| indumenti nelle diverse zone del corpo.

--| Cercheremo di adottare un sistema che sia al contempo coerente, intuitivo e
--| facile da usare, e che consenta l'interazione con i più svariati tipi di
--| indumenti presenti nel gioco.

--| La seguente tabella riassume tali scelte.

--| 
--| .Valori vestiario in EGA
--| [.center,cols="40d,6*>10m",options="header,autowidth"]
--| |==================================================================
--| | Indumento        | testa | viso | tronco | gambe | piedi | mani
--~ |--------------+-----------+------+--------+-------+-------+------+
--| | passamontagna        | 1 |    1 |      0 |     0 |     0 |    0
--| | cappello             | 2 |    0 |      0 |     0 |     0 |    0
--| | occhiali             | 0 |    2 |      0 |     0 |     0 |    0
--| | canottiera/reggiseno | 0 |    0 |      2 |     0 |     0 |    0
--| | mutande/slip         | 0 |    0 |      0 |     2 |     0 |    0
--| | teddy                | 0 |    0 |      4 |     4 |     0 |    0
--| | blusa/shirt/T-shirt  | 0 |    0 |      8 |     0 |     0 |    0
--| | dress/tuta lavoro    | 0 |    0 |      8 |    32 |     0 |    0
--| | gonna                | 0 |    0 |      0 |    32 |     0 |    0
--| | pantaloni/shorts     | 0 |    0 |      0 |    16 |     0 |    0
--| | sweater/pullover     | 0 |    0 |     16 |     0 |     0 |    0
--| | giacca               | 0 |    0 |     32 |     0 |     0 |    0
--| | cappotto             | 0 |    0 |     64 |    64 |     0 |    0
--| | calze/stockings      | 0 |    0 |      0 |     0 |     2 |    0
--| | tights/pantiehose    | 0 |    0 |      0 |     8 |     2 |    0
--| | scarpe/stivali       | 0 |    0 |      0 |     0 |     4 |    0
--| | guanti               | 0 |    0 |      0 |     0 |     0 |    2
--| |==================================================================


--~============================================================================
--~\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--~-----------------------------------------------------------------------------
--| === Gli indumenti
--~-----------------------------------------------------------------------------
--~/////////////////////////////////////////////////////////////////////////////
--~============================================================================

--| Per semplificare la gestione e lo studio del codice, raggrupperemo tutti gli
--| indumenti assieme, a prescindere di chi li indossi o dove si trovino a inizio
--| partita.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Sottoclassi di indumento specializzate
--~-----------------------------------------------------------------------------
--~=============================================================================

--| Dato che molti indumenti condividono elementi comuni, creeremo alcune
--| sottoclassi di `indumento` specializzate, e sfrutteremo l'ereditarietà per
--| semplificare la creazione e la gestione di indumenti dello stesso genere.

--| 
--| Creeremo due tipi di sottoclassi di vestiario:
--| 
--|   1. Prototipi        -- prefisso: `proto_`.
--|   2. Classi generiche -- prefisso: `cl_`.


--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Copricapi
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY cl_cappello IsA indumento.
  NAME cappello.
  HAS strato_testa 2.
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Indumenti facciali
--~-----------------------------------------------------------------------------
--~=============================================================================

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Biancheria
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY proto_biancheria IsA indumento.
  NAME biancheria.
END EVERY.
--------
-- Torso
--------
EVERY cl_canottiera IsA proto_biancheria.
  NAME canottiera.
  HAS articolo "la".
  HAS strato_tronco 2.
END EVERY.
----------
-- Inguine
----------
EVERY cl_mutande IsA proto_biancheria.
  NAME mutande.
  HAS articolo "le".
  HAS strato_gambe 2.
  INDEFINITE ARTICLE "un paio di"
END EVERY.
--------
-- Piedi
--------
EVERY cl_calzini IsA indumento.
  NAME calzini.
  HAS articolo "i".
  HAS strato_piedi 2.
  INDEFINITE ARTICLE "un paio di"
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Calzature
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY cl_scarpe IsA indumento.
  NAME scarpe.
  HAS articolo "le".
  HAS strato_piedi 4.
  INDEFINITE ARTICLE "un paio di"
END EVERY.

EVERY cl_stivali IsA indumento.
  NAME stivali.
  HAS articolo "gli".
  HAS strato_piedi 4.
  INDEFINITE ARTICLE "un paio di"
END EVERY.
--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Magliette, camicie
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY cl_tshirt IsA indumento.
  HAS strato_tronco 8.
  HAS articolo "la".
  NAME 'T-shirt'.
  NAME tshirt.
  NAME maglietta.
END EVERY.

EVERY cl_camicia IsA indumento.
  HAS strato_tronco 8.
  HAS articolo "la".
  NAME camicia.
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Maglioni
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY proto_maglione IsA indumento.
  HAS strato_tronco 16.
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Indumenti gambe
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY proto_gonna IsA indumento.
  NAME gonna.
  HAS articolo "la".
  HAS strato_gambe 32. NOT blocca_gambe.
END EVERY.

EVERY proto_collant IsA indumento.
  NAME collant.
  HAS articolo "i".
  INDEFINITE ARTICLE "un paio di"
  HAS strato_gambe 8.
END EVERY.


--~=============================================================================
--| ===== Pantaloni
--~=============================================================================
--| Dato che la classe `indumento` è definita come contenitore (per consentire
--| di avere tasche, ecc.) sfrutteremo questa funzionalità nei pantaloni, così
--| da poter testare come la libreria gestisce oggetti annidati nel vestiario.

EVERY proto_pantaloni IsA indumento.
  NAME pantaloni.
  NAME tasche. --> per catturare "metti ogg in tasche"
  HAS articolo "i".
  INDEFINITE ARTICLE "un paio di"
  HAS strato_gambe 16.
            -- ----------------------------------
  CONTAINER -- Personalizza descrizione contenuti
    HEADER  -- ----------------------------------
      "Nelle tasche dei pantaloni"
      IF COUNT IsA thing, DIRECTLY IN THIS >1
        THEN "ci sono"
        ELSE "c'è"
      END IF.  
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Tute da lavoro
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY proto_tuta IsA indumento.
  NAME tuta.
  HAS articolo "la".
  HAS strato_tronco 8.
  HAS strato_gambe 32.
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Abiti da donna (pezzo unico)
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY cl_abito IsA indumento.
  HAS articolo "l'".
  NAME abito.
  HAS strato_tronco 8.
  HAS strato_gambe 32. NOT blocca_gambe.
END EVERY.

--~=============================================================================
--~-----------------------------------------------------------------------------
--| ==== Cappotti
--~-----------------------------------------------------------------------------
--~=============================================================================

EVERY cl_cappotto IsA indumento.
  NAME cappotto.
  HAS strato_tronco 64.
  HAS strato_gambe 64. NOT blocca_gambe.
END EVERY.

--<

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- VERBI SPECIALI
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

--==============================================================================
-- MOSTRA LE USCITE
--==============================================================================

-- L'elenco delle uscite è, in molte stanze, troppo lungo e brutto da vedersi
-- nella descrizione del luogo. Quindi lascieremo che il giocatore usi il comando
-- "uscite" per visualizzare le uscite nel luogo in cui si trova.
-- Il verbo sfrutta l'attributo 'uscite_stanza' che è stato aggunto a ogni luogo. 

-- TODO: Trova un modo semplice per fare in modo che le uscite siano mostrate
--       automaticamente quando un luogo viene descritto per la prima volta.

SYNTAX mostra_uscite = uscite.

VERB mostra_uscite
  DOES
    IF uscite_stanza OF CURRENT LOCATION <> ""
      THEN "Puoi andare a:" SAY uscite_stanza OF CURRENT LOCATION.
      ELSE "Spiacente, non è disponibile la lista delle uscite per questo luogo."
    END IF.
END VERB mostra_uscite.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- ALLESTIMENTO DELE SCENOGRAFIE 
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- È tempo di popolare le stanza con oggetti ed arredamento...

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Scenografia: Entrata Emporio GA
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

--==============================================================================
-- Porte dell'Emporio
--==============================================================================
-- Delle false porte che si aprono automaticamente al passaggio dell'eroe.
-- Vengono spostate dall'entrata dell'emporio all'atrio quando l'eroe entra, e
-- viceversa quando esce; questo al fine di doverle duplicare in ambo le stanze.

THE ega_porte IsA object AT ega_entrata.
  NAME porte.
  NAME porta.
  HAS articolo "le".
  DESCRIPTION
    "Due imponenti porte in ottone"
    IF THIS AT ega_entrata
      THEN
        "attendono di condurti a nord, dentro il tempio della moda."
      ELSE
        "conducono a sud, fuori dal tempio del consumismo."
    END IF.
  MENTIONED "porte dell'emporio"
  HAS xDesc
    "Incisa sulle grosse portone d'ottone, l'aquila stilizzata del logo ""GA""
     ti scruta come un d*o che vigila su uno dei suoi umili servi.
     Le porte sono prive di maniglie, ma scorgi un piccolo sensore nascosto nel
     telaio.".
  IS NOT prendibile.
  IS NOT spostabile.
  HAS testo_attraversa
    "Come ti avvicini, le porte scorrono magicamente via per lasciarti passare.".
END THE ega_porte.

THE ega_telaio_porte IsA object AT ega_entrata.
  NAME telaio.
  NAME sensore.
  HAS articolo "l'".
  MENTIONED "entrata"
  IS scenario.
END THE ega_telaio_porte.
--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Scenografia: Atrio dell'Emporio
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

THE bancone IsA supporto AT ega_atrio.
  DESCRIPTION ""
  HAS xDesc
    "Questo lussuoso bancone in mogano è dove i clienti vanno a pagare i vestiti
     acquistati, un po' come una cassa del supermercato, ma più discreta.".
  VERB guarda_dietro
    DOES ONLY
      "Sbirciando dietro il bancone noti una pistola, devono tenerla lì per le
       emergenze."
      LOCATE pistola AT ega_atrio.
  END VERB guarda_dietro.
END THE bancone.

-- Giusto per testare alcuni verbi, nascondiamo una pistola dietro il bancone,
-- che diverrà visibile guardando dietro il bancone.

THE pistola IsA arma. -- IN cestino.
  HAS articolo "la".
  CAN sparare.
  IS nascosta.
  MENTIONED
    "pistola"
    IF THIS IS nascosta
      THEN "(nascosta dietro il bancone)"
    END IF.
  HAS xDesc "Una pistola semiautomatica, sembra carica.".

  VERB prendi
    DOES AFTER
      MAKE THIS NOT nascosta.
  END VERB prendi.
END THE pistola.


THE cestino IsA contenitore_elencato AT ega_atrio.
  -- WITH OPAQUE CONTAINER
  WITH CONTAINER
    HEADER "Nel cestino puoi vedere"
  IS NOT spostabile. NOT prendibile.
  HAS xDesc "Un cestino per la spazzatura.".

  INITIALIZE
    -- Facciamo in modo che il cesto possa contenere ogni oggetto del gioco:
    FOR EACH ogg IsA object
      DO INCLUDE ogg IN THIS:consentiti.
    END FOR.
  
  VERB esamina
    DOES ONLY
      LIST THIS.
  END VERB esamina.
END THE.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Scenografia: Camerino
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

THE sedia IsA object AT ega_camerino.
  HAS articolo "la".
  HAS xDesc "Una sedia in plastica.".
END THE.

THE cesto IsA contenitore_elencato AT ega_camerino.
  HAS xDesc "Un cesto in vimini per gettare gli indumenti scartati.".
  INITIALIZE
    -- Facciamo in modo che il cesto possa contenere ogni indumento del gioco:
    FOR EACH ind IsA indumento
      DO INCLUDE ind IN THIS:consentiti.
    END FOR.  
END THE.
--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- ATTORI PNG
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

--==============================================================================
--------------------------------------------------------------------------------
-- Il Manager
--------------------------------------------------------------------------------
--==============================================================================

-- Questo PNG lo collochiamo nell'atrio, giusto per poter fare delle prove con
-- verbi che trasferirebbero (implicitamente) indumenti indossati da un PNG ad
-- un altro.

THE manager IsA maschio AT ega_atrio.
  DESCRIPTION "Dietro al bancone c'è il manager dell'emporio."
END THE manager.

--==============================================================================
-- Abbigliamento Manager
--==============================================================================
THE calzoni_manager IsA proto_pantaloni IN manager.
  IS indossato.
  NAME calzoni.
  NAME suoi calzoni.
END THE.

THE pullover_manager IsA proto_maglione IN manager.
  IS indossato.
  NAME pullover.
  NAME suoi pullover.
END THE.

THE mocassini_manager IsA cl_scarpe IN manager.
  IS indossato.
  HAS articolo "i".
  NAME mocassini.
  NAME suoi mocassini.
END THE.

--==============================================================================
--------------------------------------------------------------------------------
-- L'Assistente Personale 
--------------------------------------------------------------------------------
--==============================================================================

-- Questo PNG non è un semplice commesso bensì il nostro "assistente personale"
-- durante la nostra visita all'emporio.
-- L'Assistente deve svolgere alcuni compiti automatizzati:
--   [x] seguire l'eroe dentro l'emporio, ma:
--       [x] non lasciare l'emporio quando l'eroe esce.
--       [x] non entrare nel camerino.
-- (Ci sono ancore alcune cose da implementare.)

-- Dato che l'emporio ha una sola entrata/uscita, l'attributo 'seguendo' dell'
-- assistente può essere controllate tramite EXIT.

THE assistente_ega IsA femmina AT ega_atrio.
  NAME assistente.
  NAME assistente personale.
  HAS articolo "l'". 
  INDEFINITE FORM "la tua assistente personale"
  HAS xDesc "È la tua assistente personale per lo shopping.".
END THE assistente_ega.

-- Impedisci all'assistente di seguirci nel camerino:
WHEN hero AT ega_camerino
  => MAKE assistente_ega NOT seguendo.
END WHEN.

-- Per riattivare la modalità "inseguimento" useremo questo evento, attivato
-- tramite le EXIT delle stanze:
EVENT Riattiva_Assistente
  -- Se l'eroe esce dal camerino in una stanza diversa da quella da cui è entrato,
  -- l'assistente dovrà raggiungerlo nella nuova destinazione:
  IF assistente_ega NOT AT hero
    THEN
      "L'assistente ti raggiunge di corsa"
      -- Stampa la preposizione articolata appropriata per il nome del luogo:
      SAY prep_DA OF location OF assistente_ega.
      -- Se il luogo ha un nome breve usiamolo:
      IF menzione_stanza OF location OF assistente_ega <> ""
        THEN SAY menzione_stanza OF location OF assistente_ega.
        ELSE SAY location OF assistente_ega.
      END IF. "."
      LOCATE assistente_ega AT hero.
  END IF.
  MAKE assistente_ega seguendo.
END EVENT.

--==============================================================================
-- Abbigliamento Assistente
--==============================================================================
-- Forniamo all'assistente dei vestiti di modo da poter testare la gestione di
-- indumenti indossati dai PNG.

THE abito_assist IsA cl_abito IN assistente_ega.
  IS indossato.
  NAME abito.
  NAME suo abito.
  HAS xDesc "È un abito a pezzo unico, elegante.".
  
END THE.

THE scarpe_assist IsA cl_stivali IN assistente_ega.
  IS indossato.
  NAME stivaletti.
  NAME suoi stivaletti.
  HAS xDesc "Un paio di stivaletti in pelle da donna, con tacco alto, arrivano
             fino alle ginocchia..".
END THE.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- CAPI DI VESTIARIO
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Ora non ci resta che creare gli indumenti con cui popolare i vari reparti
-- dell'emporio...

--==============================================================================
-- Indumenti --> Camerino (temporaneo) 
--==============================================================================
-- Per velocizzare i testi iniziali, alcuni vestiti sono stati collocati alla
-- rinfusa nel camerino, ma è una soluzione temporanea...

THE salopette IsA proto_tuta AT ega_camerino.
  NAME salopette.
  HAS xDesc
    "Una tuta in denim a pezzo unico, con pantaloni a gambe larghe e una
     pettorina con bretelle.".
END THE.

THE gonna_rossa IsA proto_gonna AT ega_camerino.
  NAME gonna rossa.
  HAS xDesc "È una gonna rossa in tessuto spesso.".
END THE.

THE collant_bianchi IsA proto_collant AT ega_camerino.
  NAME collant bianchi.
  HAS xDesc "Un paio di collant bianchi, in nylon.".
END THE.

THE blue_jeans IsA proto_pantaloni AT ega_camerino.
  NAME jeans.
  NAME blue jeans.
  HAS xDesc "Un paio di blue jeans.".
END THE.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Indumenti --> Reparto Uomo
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

THE tshirt IsA cl_tshirt AT ega_uomo.
  
END THE.

THE camicia_rossa IsA cl_camicia AT ega_uomo.
  NAME camicia rossa.
END THE.

THE camicia_nera IsA cl_camicia AT ega_uomo.
  NAME camicia nera.
END THE.

THE cappotto IsA cl_cappotto AT ega_uomo.
END THE.


--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- Indumenti --> Reparto Sportivo
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

THE passamontagna IsA indumento AT ega_sport.
  HAS strato_testa 1.
  HAS strato_viso  1.
  HAS xDesc
    "Un passamontagna invernale. Praticamente, un cappuccio con buchi per occhi
     e bocca, come quelli usati nelle rapine, hai presente?".
END THE.

THE occhiali_sci IsA indumento AT ega_sport.
  NAME occhiali.
  NAME occhiali sci.
  HAS articolo "gli".
  MENTIONED "occhiali da sci"
  HAS strato_viso 2.
  INDEFINITE ARTICLE "un paio di"
  HAS xDesc
    "Una padio di occhiali da sci, lente frontale singola (a forma di B) e una
     banda elastica per fissarli alla testa.".
END THE.

THE costume_bagno_intero IsA indumento AT ega_sport.
  NAME costume intero.
  NAME costume.
  NAME costume bagno.
  NAME costume nuoto.

  HAS strato_tronco 2.
  HAS strato_gambe  2.
  HAS xDesc "Un costume intero, per il nuoto femminile.".
  
END THE.

THE bikini IsA indumento AT ega_sport.
  HAS strato_tronco 2.
  HAS strato_gambe  2.
  IS due_pezzi. --> tronco e gambe non formano un pezzo unico! 
  HAS xDesc "Un bikini in due pezzi, per il nuoto femminile.".
END THE.

--==============================================================================
-- INDOSSABILI SENZA VALORI ZONA (ORNAMENTALI)
--==============================================================================
-- Alcuni indossabili privi di valori di copertura, e che possono quindi essere
-- indossati liberamente, senza restrizioni...

THE orologio_sportivo IsA indumento AT ega_sport.
  NAME orologio.
  NAME orologio sportivo.
  HAS articolo "l'".
  HAS xDesc
    "Un orologio sportivo digitale, con delle app per monitorare il cuore, la
     temperatura esterna, geo-localizzare, etc.".
END THE.

--------------------------------------------------------------------------------
-- Sciarpe
--------------------------------------------------------------------------------
EVERY proto_sciarpa IsA indumento.
  NAME sciarpa.
  HAS articolo "la".
END EVERY.

THE sciarpa_bianca IsA proto_sciarpa AT ega_sport.
  NAME sciarpa bianca.
  HAS xDesc "Una sciarpa bianca.".
END THE.

THE sciarpa_blu IsA proto_sciarpa AT ega_sport.
  NAME sciarpa blu.
  HAS xDesc "Una sciarpa blu.".
END THE.

THE sciarpa_nera IsA proto_sciarpa AT ega_sport.
  NAME sciarpa nera.
  HAS xDesc "Una sciarpa nera.".
END THE.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- INVENTARIO EROE
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Diamo qualche vestito di partenza in dotazione al nostro eroe (non vorremo
-- certo che entri nudo nell'Emporio Alani, giusto?)

-- TODO:

-- [ ] Aggiungi un attributo per distinguere il vestiario originale dell'eroe da
--     quello dell'emprio, così che l'assistente possa trasportare i vestiti del
--     protagonista mentre egli prova i capi del negozio. Inoltre, l'assistente
--     (o un PNG commesso) dovrà raccogliere e rimettere a posto i vestiti che
--     l'eroe sparpaglierà per l'emporio.

--==============================================================================
-- ABBIGLIAMENTO EROE
--==============================================================================

-- TODO:
-- [ ] Un attributo per indicare i vestiti posseduti (inizili o acquistati) sì
--     semplificare alcuni aspetti comuni tramite inizializzazione?
-- [ ] L'ordine di creazione conta: nel gioco, i capi verrano elencati in ordine
--     di apparazione inverso rispetto al codice.

THE calzini_eroe IsA cl_calzini IN hero.
  IS indossato.
  NAME calzini.
  NAME calze.
  NAME mie tue calze bianche.
  NAME miei tuoi calzini bianchi.
  INDEFINITE ARTICLE "i tuoi"
  DEFINITE ARTICLE "i tuoi"
  HAS xDesc
    "Un paio di calzini bianchi cui il tempo ha donato sfumature color nicotina.
     Il calzino destro ha un buco grosso abbastanza da far sbucare l'alluce.".
END THE.

THE boxer_eroe IsA cl_mutande IN hero.
  IS indossato.
  NAME boxer.
  NAME miei tuoi boxer bianchi.
  HAS articolo "i".
  INDEFINITE ARTICLE "i tuoi"
  DEFINITE ARTICLE "i tuoi"
  HAS xDesc "Sono solo un paio di boxer bianchi, mutande comode da indossare.".
END THE.

SYNONYMS boxers = boxer.

THE canottiera_eroe IsA cl_canottiera IN hero.
  IS indossato.
  NAME canottiera.
  NAME mia tua canottiera bianca.
  INDEFINITE ARTICLE "la tua"
  DEFINITE ARTICLE "la tua"
  HAS xDesc "Una semplice canottiera bianca, qualche macchia d'unto qui e là.".
END THE.

THE scarpe_eroe IsA cl_scarpe IN hero.
  IS indossato.
  NAME sneakers.
  NAME sneaker.
  NAME mie tue sneakers nere.
  INDEFINITE ARTICLE "le tue"
  DEFINITE ARTICLE "le tue"
  HAS xDesc
    "Le tue care vecchie sneakers nere, delle Converse originali, decisimante logore.".
END THE.

--------------------------------------------------------------------------------
-- Pantaloni
--------------------------------------------------------------------------------
-- I pantaloni hanno "tasche" (in realtà, sono solo contenitori, come tutti gli
-- indumenti), e noi le sfrutteremo per testare oggetti annidati nei vestiti.

THE pantaloni_eroe IsA proto_pantaloni IN hero.
  IS indossato.
  NAME pantaloni.
  NAME miei tuoi pantaloni viola.
  INDEFINITE ARTICLE "i tuoi"
  DEFINITE ARTICLE "i tuoi"
  HAS xDesc "Un paio di pantaloni in cotone, color viola.".
END THE pantaloni_eroe.

-- Mettiamo degli oggetti nei pantaloni così da poter testare come la libreria
-- gestisce oggetti annidati nel vestiario:

-- -----------
-- Portafoglio
-- -----------
THE portafoglio_eroe IsA object IN pantaloni_eroe.
  NAME portafoglio.
  NAME mio portafoglio.
  INDEFINITE ARTICLE "il tuo"
  DEFINITE ARTICLE "il tuo"
  HAS xDesc "Un banale portafoglio in pelle.".
END THE.

SYNONYMS portafogli = portafoglio.

-- ------
-- Kippah
-- ------
THE kippah_eroe IsA indumento IN pantaloni_eroe.
  NAME kippah.
  NAME mia kippah.
  HAS strato_testa 2.
  HAS articolo "la".
  INDEFINITE ARTICLE "la tua"
  DEFINITE ARTICLE "la tua"
  HAS xDesc "La tua kippah, la porti con te ovunque tu vada.".
END THE.

SYNONYMS kippà, yarmulke = kippah.

--------------------------------------------------------------------------------
START AT ega_entrata.
DESCRIBE intestazione.

"$pDigita" STYLE EMPHASIZED. "USCITE" STYLE NORMAL.
  "per visualizzare la lista di uscite disponibili in una stanza."
"$nUsa il verbo" STYLE EMPHASIZED. "DBG INDUMENTO" STYLE NORMAL.
  "per mostrare i valori di copertura di un capo di vestiario."

-- EOF --

--==============================================================================
-- "La Casa" by Tristano Ajmone, 2018.
--==============================================================================
IMPORT 'libreria.i'. -- ALAN Libreria Standard Italiana v2.1


THE mia_AT IsA blocco_definizioni
  ------------------------------------------------------------------------------
  HAS       titolo  "La Casa".
  HAS  sottotitolo  "Un set per eseguire test su stanze e luoghi esterni.".
  HAS       autore  "Tristano Ajmone".
  HAS         anno   2018.
  HAS     versione  "1".
  ------------------------------------------------------------------------------
END THE.

-- =============================================================================

-- STANZA: La Cucina

-- =============================================================================
-- La cucina sarà il nostro luogo per testare la classe 'stanza'.

THE cucina IsA stanza.
  NAME 'La Cucina'.
  EXIT sud TO giardino.
  DESCRIPTION "A sud c'è il tuo giardino."
END THE cucina.

-- =============================================================================

-- LUOGO ESTERNO: Il Giardino

-- =============================================================================
-- Il giardino sarà il nostro luogo per testare la classe 'luogo_esterno'.

THE giardino IsA luogo_esterno.
  NAME 'Il Giardino di Casa'.
  EXIT nord TO cucina.
  DESCRIPTION "A nord puoi tornare in casa."

  VERB ascolta0
    DOES ONLY "Puoi udire un cinguettio."
  END VERB ascolta0.
END THE giardino.

-- =============================================================================

-- Scenografia Cucina

-- =============================================================================

--==============================================================================
-- La Sedia
--==============================================================================
-- Creiamo una sedia che sia utilizzabile.

THE sedia IsA supporto AT cucina.
  HAS articolo "la".
  
  VERB siediti_su
    DOES ONLY
      IF hero IS sdraiato
        THEN "Ti alzi e"
          MAKE hero NOT sdraiato.
      END IF.
      "ti siedi" SAY superficie:prep_SU. SAY superficie. "."
      MAKE hero seduto.
  END VERB siediti_su.
END THE sedia.


--==============================================================================
-- Il Frigorifero (contenitore_elencato + apribile + peso 50)
--==============================================================================
THE frigo IsA contenitore_elencato AT cucina.
  OPAQUE CONTAINER -- NOTE: The library doesn't initialize closed listed containers
                   --       to be automatically opaque!
  IS apribile.
  IS NOT aperto.
  HAS peso 50. -- Peso >= 50 impedisce i verbi 'prendi', 'prendi_da' e 'solleva'.
  

  INITIALIZE -- Consenti al frigo di contenere qualsiasi alimento del gioco:
    FOR EACH oAlimento IsA alimento
      DO INCLUDE oAlimento IN consentiti OF THIS.
    END FOR.
  
END THE frigo.
--==============================================================================
-- Il Tavolo da Cucina (supporto)
--==============================================================================

THE tavolo_cucina IsA supporto AT cucina.
  IS NOT prendibile.
  NAME tavolo.
END THE tavolo_cucina.

--==============================================================================
-- Frutta Basket (listed container on tavolo)
--==============================================================================

THE cesto_frutta IsA cont_alimenti IN tavolo_cucina.
  NAME cesto.
  MENTIONED "cesto per la frutta"
END THE cesto_frutta.

-- =============================================================================

-- Scenografia Giardino

-- =============================================================================

--==============================================================================
-- La Staccionata
--==============================================================================
-- La staccionata è un escamotage per poter mettere nel giardino cose e attori
-- non raggiungibili. L'idea è che anche se li si può vedere sono comunque non
-- raggiungibili (ma non distanti).

THE staccionata IsA object AT giardino.
  HAS articolo "la".
  DESCRIPTION
    "Oltre la staccionata intravedi un ombrellone e, in lontananza,
     la tua vicina."
END THE staccionata.

--==============================================================================
-- L'ombrellone
--==============================================================================
-- È un oggetto non raggiungibile, utile per testare svariati verbi.

THE ombrellone IsA object AT giardino.
  NAME ombrellone.
  NAME 'l''ombrellone'.
  HAS articolo "l'".
  IS NOT raggiungibile.
END THE ombrellone.

--==============================================================================
-- L'aquilone
--==============================================================================
-- Un oggetto distante...
THE aquilone IsA object AT giardino.
  NAME aquilone.
  NAME 'l''aquilone'.
  HAS articolo "l'".
  IS distante.
END THE aquilone.

--==============================================================================
-- Il prato (/erba)
--==============================================================================
-- Creiamo il prato solo perché ci serve un oggetto scenario da testare.

THE prato IsA object AT giardino.
  IS scenario.
  NAME prato.
  NAME erba.
END THE prato.

--==============================================================================
-- Il Cinguettio
--==============================================================================
-- Un 'suono' per testare sia 'ascolta' che con oggetti che NON sono:
--    esaminabile, prendibile, raggiungibile, spostabile.
THE cinguettio IsA suono AT giardino.
  DESCRIPTION "" -- non vogliamo che compaia nella descrizione (compare se si
                 -- 'ascolta' in giardino).

  VERB ascolta
    DOES ONLY
      IF hero AT ogg
        THEN "Si direbbe il canto della cinciallegra."
      ELSIF ogg NEAR hero
        THEN "Da questa distanza è difficile capire di che uccelli si tratti."
      END IF.
  END VERB ascolta.
END THE cinguettio.

--==============================================================================
-- Il Tavolo da Giardino (supporto)
--==============================================================================

THE tavolo_giardino IsA supporto AT giardino.
  IS NOT prendibile.
  NAME tavolo.
END THE tavolo_giardino.

--------------------------------------------------------------------------------
-- La Rivista
--------------------------------------------------------------------------------
THE rivista IsA object IN tavolo_giardino.
  HAS articolo "la".
  -- @TODO: implementala come leggibile!
END THE rivista.


--==============================================================================
-- Il Lettino
--==============================================================================
-- Creiamo un lettino su cui ci si possa sdraiare così da poter testare il verbo
-- 'sdraiati' ed i messaggi dei CHECK che bloccano le azioni intentate quando il
-- protagonista è sdraiato.

THE lettino IsA supporto AT giardino.
  NAME lettino.
  NAME letto.

  HAS xDesc "È un lettino pieghevole, di quelli usati in campeggio.".
  
  VERB sdraiati_su
    DOES ONLY
      IF hero IS seduto
        THEN "Ti alzi e"
          MAKE hero NOT seduto.
      END IF.
      "ti sdrai" SAY superficie:prep_SU. SAY superficie. "."
      MAKE hero sdraiato.
  END VERB sdraiati_su.
END THE lettino.

--==============================================================================
-- La Piscina
--==============================================================================
-- Creiamo una piscina per testare sia 'nuota' che 'entra'.

THE piscina IsA liquido AT giardino.
  HAS articolo "la".
  NAME piscina.
  NAME acqua.
  NAME 'l''acqua'.

  VERB nuota
    DOES ONLY
      IF hero DIRECTLY AT nella_piscina
        THEN "Sei già in piscina."
        ELSE
          LOCATE hero AT nella_piscina.
      END IF.
  END VERB nuota.
    
  VERB entra_in
    DOES ONLY
      IF hero DIRECTLY AT nella_piscina
        THEN "Sei già in piscina."
        ELSE
          LOCATE hero AT nella_piscina.
      END IF.
  END VERB entra_in.

  VERB esci_da
    DOES ONLY
      IF hero NOT DIRECTLY AT nella_piscina
        THEN "Non sei dentro la piscina."
        ELSE
          "Ti issi sul bordo della piscina ed esci."
          LOCATE hero AT giardino.
      END IF.
  END VERB esci_da.
END THE piscina.

--------------------------------------------------------------------------------
-- Dentro La Piscina
--------------------------------------------------------------------------------
-- Per simulare quando il protagonista entra nella piscina creiamo un luogo
-- annidato nel giardino.

THE nella_piscina IsA location AT giardino.
  NAME 'Nella Piscina (Giardino di Casa)'.
  DESCRIPTION
    "La piscina è abbastanza ampia per fare delle piccole nuotate."

  ENTERED
    "Ti tuffi a bomba nella piscina."
END THE nella_piscina.

-- =============================================================================

-- Cibi & Bevande (Commestibili & Potabili)

-- =============================================================================
-- Creiamo alcune classi per gli oggetti di tipo 'commestibile' e 'potabile' al
-- fine di poter testare i cibi solidi e liquidi. La gerarchia delle sottoclassi
-- ci consentirà di impostare facilmente quali tipi di alimenti possano essere
-- messi nei vari contenitori, e consentono anche di testare la disambiguazione.
--==============================================================================
-- Classi di Alimenti
--==============================================================================
-- La classe 'alimento' è riservata ai cibi solidi, 'bevanda' a quelli liquidi.

EVERY alimento IsA object.
  HAS articolo "l'".
  NAME alimento.
  IS commestibile.
END EVERY.

EVERY bevanda IsA liquido.
  HAS articolo "la".
  IS potabile.
  NAME bevanda.
END EVERY.

EVERY snack IsA alimento.
  HAS articolo "lo".
  NAME snack.
END EVERY.
--------------------------------------------------------------------------------
-- Sottoclassi Alimentari
--------------------------------------------------------------------------------
EVERY frutto IsA alimento.
  HAS articolo "il".
  NAME frutto.
END EVERY frutto.
--------------------------------------------------------------------------------
-- Contenitori per Alimenti (solidi)
--------------------------------------------------------------------------------
-- Creiamo una classe specializzata per i contenitori destinati ai cibi solidi,
-- al fine di semplificare i vari test...

EVERY cont_alimenti IsA contenitore_elencato.
  NAME contenitore.

  INITIALIZE -- Consenti ai 'cont_alimenti' di contenere qualsiasi alimento del gioco:
    FOR EACH oAlimento IsA alimento
      DO INCLUDE oAlimento IN consentiti OF THIS.
    END FOR.
END EVERY cont_alimenti.
--------------------------------------------------------------------------------
-- Contenitori per Bevande
--------------------------------------------------------------------------------
-- Creiamo una classe specializzata per i contenitori destinati alle bevande, al
-- fine di semplificare il travaso di liquidi da un contenitore all'altro...

EVERY cont_bevanda IsA contenitore_elencato.
  NAME contenitore.

  INITIALIZE -- Consenti ai 'cont_bevanda' di contenere qualsiasi bevanda del gioco:
    FOR EACH oBevanda IsA bevanda
      DO INCLUDE oBevanda IN consentiti OF THIS.
    END FOR.
END EVERY cont_bevanda.
--------------------------------------------------------------------------------
-- Contenitori per Snack
--------------------------------------------------------------------------------
-- I contenitori per snack sono finalizzati a contenere stuzzichini alimentari di
-- piccole dimensioni (patatine, olive, ecc.) ma non cibi grossi (come la frutta,
-- ecc.). Sono utili a testare le risposte dei verbi che consento di mettere
-- oggetti nei contenitori.

EVERY cont_snack IsA contenitore_elencato.
  NAME contenitore.
  
  INITIALIZE -- Consenti ai 'cont_snack' di contenere qualsiasi snack del gioco:
    FOR EACH oSnack IsA snack
      DO INCLUDE oSnack IN consentiti OF THIS.
    END FOR.
END EVERY cont_snack.

--==============================================================================
-- ISTANZE ALIMENTI: Frutta
--==============================================================================
-- Mettiamo un po' di frutta nel cesto per la frutta...

THE mela IsA frutto IN cesto_frutta.
  HAS articolo "la".
  NAME mela.
END THE.

THE banana IsA frutto IN cesto_frutta.
  HAS articolo "la".
  NAME banana.
END THE.

THE pera IsA frutto IN cesto_frutta.
  HAS articolo "la".
  NAME pera.
END THE.
--==============================================================================
-- ISTANZE BEVANDE
--==============================================================================

--------------------------------------------------------------------------------
-- Vasetto Vuoto
--------------------------------------------------------------------------------
THE vasetto IsA cont_bevanda IN tavolo_cucina.
  NAME vasetto.
  HAS consentiti {vino}.
END THE.
--------------------------------------------------------------------------------
-- Bottiglia di Vino
--------------------------------------------------------------------------------
THE bottiglia IsA cont_bevanda IN tavolo_cucina.
  NAME bottiglia.
  HAS articolo "la".
END THE.

THE vino IsA liquido IN bottiglia.
  HAS vessel bottiglia. 
  INDEFINITE ARTICLE "del"
END THE.
--==============================================================================
-- ISTANZE SNACK
--==============================================================================
-- TODO: implemtalo come un contenitore mono-apribile (ossia, una volta aperto
--       il sacchetto è rotto e non lo si può richiudere).
THE sacchetto IsA cont_snack IN tavolo_cucina.
  NAME sacchetto.
END THE sacchetto.

THE patatine IsA snack IN sacchetto.
  HAS articolo "le".
  INDEFINITE ARTICLE "delle"
END THE patatine.

-- =============================================================================

-- ARMI

-- =============================================================================
-- Qualche arma bianca e da fuoco per testare i verbi che le riguardano.

THE coltello IsA arma IN tavolo_cucina.
  HAS xDesc "Un grosso coltello, molto affilato.".
END THE coltello.

THE pistola IsA arma IN tavolo_cucina.
  NAME pistola.
  NAME rivoltella.
  HAS articolo "la".
  CAN sparare.
  HAS xDesc "È una rivoltella.".
END THE pistola.

-- =============================================================================

-- ATTORI

-- =============================================================================
-- Qualche attore di base per testare i verbi...

THE giardiniere IsA maschio AT giardino.
  HAS xDesc "Il giardiniere è intento a curare le piante.".
END THE giardiniere.

THE gatto IsA actor AT giardino.
END THE gatto.

--------------------------------------------------------------------------------
-- La vicina di casa
--------------------------------------------------------------------------------
-- La vicina è al di là della staccionata e in lontananza. Quindi non sarà
-- possibile parlarle, lanciarle cose, ecc.

THE vicina IsA femmina AT giardino.
  IS NOT raggiungibile.
  IS distante.
  DESCRIPTION "" -- La descriviamo assieme alla staccionata!
  HAS xDesc "È la tua vicina di casa, ma non sai come si chiama.".
END THE vicina.


--------------------------------------------------------------------------------
START AT cucina.
DESCRIBE intestazione.

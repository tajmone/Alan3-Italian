-- Alan Italian 18 Alpha | Alan 3.0beta6 build 1880
--==============================================================================
-- "La Casa" by Tristano Ajmone, 2018.
--==============================================================================
OPTIONS WIDTH 81.

IMPORT 'libreria.i'.   -- ALAN Libreria Standard Italiana v2.1
IMPORT '../inc_debaca.i'. -- Comandi per il debugging


THE mia_AT IsA blocco_definizioni
  ------------------------------------------------------------------------------
  HAS       titolo  "La Casa".
  HAS  sottotitolo  "Un set per eseguire test su stanze e luoghi esterni.".
  HAS       autore  "Tristano Ajmone".
  HAS         anno   2018.
  HAS     versione  "1".
  ------------------------------------------------------------------------------
END THE.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- MAPPA DEL GIOCO
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================
-- Le varie stanze e luoghi esterni dell'avventura.
-- Per rendere più leggibili il codice, l'arredamento delle varie stanze viene
-- definitio in seguito, eccetto per porte, finistre ed eventuali chiavi.

-- =============================================================================

-- LA CUCINA (stanza)

-- =============================================================================
-- La cucina sarà il nostro luogo per testare la classe 'stanza'.

THE cucina IsA stanza.
  NAME 'LA CUCINA'.
  EXIT sud TO giardino.
  EXIT est TO ingresso.
  DESCRIPTION "A sud c'è il tuo giardino, a est l'ingresso."
END THE cucina.

-- =============================================================================

-- LO STUDIO (stanza)

-- =============================================================================
-- Lo studio è un luogo per testare leggere e scrivere.

THE studio IsA stanza.
  NAME 'LO STUDIO'.
  EXIT ovest TO ingresso.
  DESCRIPTION "A ovest c'è l'ingresso."
END THE studio.

-- =============================================================================

-- IL GIARDINO (luogo esterno)

-- =============================================================================
-- Il giardino sarà il nostro luogo per testare la classe 'luogo_esterno'.

THE giardino IsA luogo_esterno.
  NAME 'IL GIARDINO DI CASA'.
  EXIT nord TO cucina.
  DESCRIPTION "A nord puoi tornare in casa."

  VERB ascolta0
    DOES ONLY
      "[[ istanza 'giardino' --> VERB 'ascolta0' --> DOES ONLY ]]$n
      Puoi udire un cinguettio."
  END VERB ascolta0.
END THE giardino.


-- =============================================================================

-- L'INGRESSO

-- =============================================================================

THE ingresso IsA stanza.
  NAME 'L''INGRESSO DI CASA'.
  EXIT ovest TO cucina.
  EXIT est   TO studio.
  EXIT nord  TO vialetto
    CHECK chiave_casa_mia IN hero
      ELSE
        "[[ istanza 'ingresso' --> EXIT 'nord' ]]
         $nUscire di casa senza portarti dietro la chiave è una pessima idea."
    -- Verifica che la porta sia aperta:
    AND porta_casa_tuaR IS aperto
      ELSE
        "[[ istanza 'ingresso' --> EXIT 'nord' ]]
         $nDovresti aprire la porta prima."
  END EXIT.
  EXIT su TO corridoio
    DOES
      locate scale_casa AT corridoio.
  END EXIT.
  DESCRIPTION
    "Ti trovi nell'ingresso della tua casa. A ovest c'è la cucina, a est il tuo
     studio."
END THE ingresso.

--------------------------------------------------------------------------------
-- LE SCALE
--------------------------------------------------------------------------------
-- NOTA: Quando il giocatore si sposta dall'ingresso al corridoio (e viceversa),
--       le scale vengono spostate dalla stanza d'origine a quella di destinazione.
--       Questo crea l'illusione di un oggetto presente in entrambi i luoghi.
--       La dislocazione delle scale è gestita dal blocco EXIT di ingresso e
--       corridoio.
THE scale_casa IsA object AT ingresso.
  NAME scale.
  NAME rampa.
  HAS articolo "le".
  DESCRIPTION
    "Una rampa di scale conduce"
    IF THIS AT ingresso
      THEN "su al primo piano."
      ELSE "giù al pianterreno."
    END IF.
END THE scale_casa.

--------------------------------------------------------------------------------
-- LA PORTA DI CASA (LATO ANTERIORE)
--------------------------------------------------------------------------------
-- NOTA: La chiave della porta è definita nell'inventario dell'eroe.

THE porta_casa_tuaR IsA porta_casa AT ingresso.
  NAME 'porta di casa'.
  IS NOT bloccato. -- (dato che l'eroe è in casa).
  HAS chiave_abbinata chiave_casa_mia.
  DESCRIPTION "A nord, oltre la porta di casa, c'è il mondo esterno."
  
  HAS altro_lato porta_casa_tuaF.
  HAS xDesc
    "È la porta di casa tua, conduce alla strada.".
END THE porta_casa_tuaR.



-- =============================================================================

-- IL CORRIDOIO (PRIMO PIANO)

-- =============================================================================
THE corridoio IsA stanza.
  NAME 'CORRIDOIO (PRIMO PIANO)'.
  EXIT est TO camera_letto.
  EXIT giù TO ingresso
    DOES
      locate scale_casa AT ingresso.
  END EXIT.

  DESCRIPTION "A est c'è la tua camera da letto."
  
END THE corridoio.



-- =============================================================================

-- LA CAMERA DA LETTO (PRIMO PIANO)

-- =============================================================================
THE camera_letto IsA stanza.
  NAME 'CAMERA DA LETTO (PRIMO PIANO)'.
  EXIT est  TO corridoio.
  EXIT nord TO guardaroba.
  DESCRIPTION "A nord c'è il tuo guardaroba."
  
END THE camera_letto.


-- =============================================================================

-- IL GUARDAROBA (PRIMO PIANO)

-- =============================================================================
-- Una stanza per testare il vestiario.

THE guardaroba IsA stanza.
  NAME 'GUARDAROBA (PRIMO PIANO)'.
  EXIT sud  TO camera_letto.
  DESCRIPTION "A sud puoi tornare nella tua camera da letto."
  
END THE guardaroba.




-- =============================================================================

-- IL VIALETTO

-- =============================================================================
-- Il vialetto di casa è il punto d'accesso al mondo esterno uscendo di casa...

THE vialetto IsA luogo_esterno.
  NAME 'DAVANTI A CASA TUA'.
  EXIT sud TO ingresso
    CHECK porta_casa_tuaF IS aperto
    ELSE
      "[[ istanza 'vialetto' --> EXIT 'sud' ]]
       $nLa porta di casa tua è chiusa."
  END EXIT.
  DESCRIPTION "Ti trovi nel vialetto di fronte a casa tua."
END THE vialetto.

-- Porta di casa (fronte):
THE porta_casa_tuaF IsA porta_casa AT vialetto.
  NAME 'porta di casa tua'.
  IS NOT bloccato.
  DESCRIPTION "" -- nascondiamo la descrizione

  -- HAS altro_lato porta_casa_tuaR.
  HAS xDesc
    "È la porta di casa tua.".
END THE porta_casa_tuaF.

-- TODO: Implementa casa fittizia esamibaile.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- INVENTARIO EROE 
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Diamo qualche oggetto in dotazione al nostro eroe ...

--==============================================================================
-- CHIAVE DI CASA
--==============================================================================


THE chiave_casa_mia IsA object IN hero.
  NAME chiave.
  HAS articolo "la".
  HAS xDesc "È la chiave della tua casa".
  
END THE chiave_casa_mia.

--==============================================================================
-- LA TORCIA (fonte_di_luce)
--==============================================================================
-- Facciamo in modo che sia possibile romperla lanciandola, e anche ripararla.

THE torcia IsA fonte_di_luce IN hero.
  HAS articolo "la".
  HAS xDesc "Una torcia elettrica, ""made in China"".".
  
  VERB lancia
    DOES AFTER
      "$p[[ istanza 'torcia' --> VERB 'lancia' --> DOES AFTER ]]$n"
      IF THIS IS illuminato
        THEN "Dopo la caduta la torcia si è spenta ... un brutto segno."
        ELSE "La torcia sembra aver preso una bella botta, speriamo in bene."
      END IF.
      MAKE THIS rotto.
      MAKE THIS NOT illuminato.
  END VERB lancia.

  VERB ripara
    DOES ONLY
      "[[ istanza 'torcia' --> VERB 'ripara' --> DOES ONLY ]]
      $nDai qualche colpetto alla torcia e questa torna a funzionare."
      MAKE THIS NOT rotto.
  END VERB ripara.
END THE torcia.

--==============================================================================
-- Abbigliamento Iniziale del Protagonista
--==============================================================================

THE hero IsA ACTOR.
  --
END THE hero.

THE bombetta IsA ind_cappello IN hero.
  IS indossato.
  NAME bombetta.
  HAS articolo "la".
  HAS xDesc "È una bombetta originale inglese.".
END THE bombetta.

THE jeans IsA ind_pantaloni IN hero.
  IS indossato.
  NAME jeans.
  NAME blue jeans.
  NAME blu jeans.
  HAS articolo "il". ---> sovrascriviamo l'articolo "i" della classe 'pantaloni'!
  MENTIONED "paio di blue jeans"
END THE jeans.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- CLASSI SPECIALIZZATE
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Per semplificarci la vita creeremo delle classi specializzati per alcuni tipi
-- di oggetti..


--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- PORTE, PORTONI E CANCELLI 
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

--------------------------------------------------------------------------------
-- Porte delle case
--------------------------------------------------------------------------------
-- Le porte delle case sono bloccabili e bloccate di default.

EVERY porta_casa IsA porta.
  NAME porta.
  HAS articolo "la".
  IS bloccabile.
  IS bloccato.
END EVERY porta_casa.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- VESTIARIO
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

-- Gli indumenti devono rispettare i valori della Tabella del Vestiario:

--+--------------------------+------+--------+-------+-------+------+
--| Indumento        | testa | viso | tronco | gambe | piedi | mani |
--+--------------------------+------+--------+-------+-------+------+
--| cappello               2 |    0 |      0 |     0 |     0 |    0 |
--| canottiera/reggiseno   0 |    0 |      2 |     0 |     0 |    0 |
--| undies/panties         0 |    0 |      0 |     2 |     0 |    0 |
--| teddy                  0 |    0 |      4 |     4 |     0 |    0 |
--| blouse/shirt/T-shirt   0 |    0 |      8 |     0 |     0 |    0 |
--| dress/coveralls        0 |    0 |      8 |    32 |     0 |    0 |
--| gonna                  0 |    0 |      0 |    32 |     0 |    0 |
--| pantaloni/shorts       0 |    0 |      0 |    16 |     0 |    0 |
--| sweater/pullover       0 |    0 |     16 |     0 |     0 |    0 |
--| giacca                 0 |    0 |     32 |     0 |     0 |    0 |
--| cappotto               0 |    0 |     64 |    64 |     0 |    0 |
--| socks/stockings        0 |    0 |      0 |     0 |     2 |    0 |
--| tights/pantiehose      0 |    0 |      0 |     8 |     2 |    0 |
--| scarpe/stivali         0 |    0 |      0 |     0 |     4 |    0 |
--| guanti                 0 |    0 |      0 |     0 |     0 |    2 |
--+--------------------------+------+--------+-------+-------+------+

-- Inoltre, gli indumenti possono essere associati a un `genere` specifico, dove
-- un valore `0` indica un genere neutro (unisex), e qualsiasi altro valore
-- ne limiterà l'indossabilità al genere dell'eroe.
-- 
-- In questa avventura, adopereremo i seguenti valori di genere:
-- 
--   * `1` genere maschile.
--   * `2` genere femminile.

--==============================================================================
-- Indumenti Testa
--==============================================================================
EVERY ind_cappello IsA indumento.
  NAME cappello.
  HAS strato_testa 2.
END EVERY.
--==============================================================================
-- Biancheria
--==============================================================================
EVERY ind_canottiera IsA indumento.
  NAME canottiera.
  HAS articolo "la".
  HAS strato_tronco 2.
END EVERY.

EVERY ind_mutande IsA indumento.
  NAME mutande.
  HAS articolo "le".
  HAS strato_gambe 2.
END EVERY.

EVERY ind_calze IsA indumento.
  NAME calze.
  HAS articolo "le".
  HAS strato_piedi 2.
END EVERY.

EVERY ind_reggiseno IsA indumento.
  NAME reggiseno.
  HAS strato_tronco 2.
  HAS genere 2.
END EVERY.

--==============================================================================
-- Vestiti
--==============================================================================
EVERY ind_pantaloni IsA indumento.
  NAME pantaloni.
  HAS articolo "i".
  HAS strato_gambe 16.
END EVERY.

EVERY ind_camicia IsA indumento.
  NAME camicia.
  HAS articolo "la".
  HAS strato_tronco 8.
END EVERY.

EVERY ind_maglione IsA indumento.
  NAME maglione.
  HAS strato_tronco 16.
END EVERY.


  -- HAS strato_testa  0.
  -- HAS strato_tronco 0.
  -- HAS strato_gambe  0.
  -- HAS strato_piedi  0.
  -- HAS strato_mani   0.
--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- CIBI & BEVANDE (commestibili & potabili)
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

-- Creiamo alcune classi per gli oggetti di tipo 'commestibile' e 'potabile' al
-- fine di poter testare i cibi solidi e liquidi. La gerarchia delle sottoclassi
-- ci consentirà di impostare facilmente quali tipi di alimenti possano essere
-- messi nei vari contenitori, e consentono anche di testare la disambiguazione.

--==============================================================================

-- CLASSI DI ALIMENTI

--==============================================================================
-- La classe 'alimento' è riservata ai cibi solidi, 'bevanda' a quelli liquidi.

EVERY alimento IsA object.
  HAS articolo "l'".
  NAME alimento.
  IS commestibile.
END EVERY.

EVERY bevanda IsA liquido.
  HAS articolo "la".
  IS potabile.
  NAME bevanda.
END EVERY.

EVERY snack IsA alimento.
  HAS articolo "lo".
  NAME snack.
END EVERY.
--------------------------------------------------------------------------------
-- Sottoclassi alimentari
--------------------------------------------------------------------------------
EVERY frutto IsA alimento.
  HAS articolo "il".
  NAME frutto.
END EVERY frutto.
--==============================================================================

-- CLASSI DI CONTENITORI PER ALIMENTI

--==============================================================================
-- Definiamo delle classi di contenitori idonei a contenere i vari generi
-- alimentari...

--------------------------------------------------------------------------------
-- Contenitori per alimenti (cibi solidi)
--------------------------------------------------------------------------------
-- Creiamo una classe specializzata per i contenitori destinati ai cibi solidi,
-- al fine di semplificare i vari test...

EVERY cont_alimenti IsA contenitore_elencato.
  NAME contenitore.

  INITIALIZE -- Consenti ai 'cont_alimenti' di contenere qualsiasi alimento del gioco:
    FOR EACH oAlimento IsA alimento
      DO INCLUDE oAlimento IN consentiti OF THIS.
    END FOR.
END EVERY cont_alimenti.
--------------------------------------------------------------------------------
-- Contenitori per bevande
--------------------------------------------------------------------------------
-- Creiamo una classe specializzata per i contenitori destinati alle bevande, al
-- fine di semplificare il travaso di liquidi da un contenitore all'altro...

EVERY cont_bevanda IsA contenitore_elencato.
  NAME contenitore.

  INITIALIZE -- Consenti ai 'cont_bevanda' di contenere qualsiasi bevanda del gioco:
    FOR EACH oBevanda IsA bevanda
      DO INCLUDE oBevanda IN consentiti OF THIS.
    END FOR.
END EVERY cont_bevanda.
--------------------------------------------------------------------------------
-- Contenitori per snack
--------------------------------------------------------------------------------
-- I contenitori per snack sono finalizzati a contenere stuzzichini alimentari di
-- piccole dimensioni (patatine, olive, ecc.) ma non cibi grossi (come la frutta,
-- ecc.). Sono utili a testare le risposte dei verbi che consento di mettere
-- oggetti nei contenitori.

EVERY cont_snack IsA contenitore_elencato.
  NAME contenitore.
  
  INITIALIZE -- Consenti ai 'cont_snack' di contenere qualsiasi snack del gioco:
    FOR EACH oSnack IsA snack
      DO INCLUDE oSnack IN consentiti OF THIS.
    END FOR.
END EVERY cont_snack.


--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- OGGETTI SCRIVIBILI E STRUMENTI PER LA SCRITTURA
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================
-- Poiché il verbo alla vaniglia 'scrivi' della libreria non porta a termine
-- l'azione, creeremo una classe apposita su cui reimplementeremo 'scrivi' di
-- modo che la stringa del parametro venga aggiunta al testo dell'oggetto.

EVERY ogg_scrivibile IsA object.
  IS scrivibile.

  VERB scrivi
    WHEN ogg
      -- Verifichiamo che l'eroe possieda almeno un oggetto con cui scrivere:
      CHECK COUNT IsA ogg_scrittura, DIRECTLY IN hero > 0
        ELSE
          "[[ classe 'ogg_scrivibile' --> VERB 'scrivi' --> CHECK ]]
          $nNon hai nulla con cui scrivere."
      DOES ONLY
        "[[ classe 'ogg_scrivibile' --> VERB 'scrivi' --> DOES ONLY ]]$n"
        IF ogg:testo = ""
          THEN SET ogg:testo TO testo.
          ELSE SET ogg:testo TO ogg:testo + " " + testo.
        END IF.
        "Hai scritto ""$1""" SAY ogg:prep_SU. "$2."
        MAKE ogg leggibile.
  END VERB scrivi.
END EVERY ogg_scrivibile. 

-- Creiamo anche una classe generica per gli oggetti idonei alla scrittura...
EVERY ogg_scrittura IsA object.
  
END EVERY ogg_scrittura.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- ATTORI DINAMICI 
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Alcuni attori con copioni che li fanno spostare nel mondo del gioco...


--==============================================================================
-- MR NICKY (attore: cane)
--==============================================================================
-- "Mr Nicky" è il mio adorato cagnolino nella vita reale, quindi perché non
-- infilarlo in quest'avventura?

THE nicky IsA actor AT cucina.
  NAME 'Mr Nicky'.
  NAME nicky.
  HAS nome_proprio.
  IS seguendo. -- Nicky seguirà l'eroe ovunque esso vada!
  HAS xDesc "È Mr Nicky, il tuo amato nonché fedele amico a quattro zampe.".

  -- Esercitiamo un po' di controllo dinamico sulla sua descrizione:
  IS NOT già_descritto.
  DESCRIPTION
    IF THIS IS NOT già_descritto
        THEN "Al centro della stanza c'è Mr Nicky, il tuo cagnolino."
        MAKE THIS già_descritto.
      ELSE
        DEPENDING ON RANDOM 1 TO 3
          = 1 THEN
            "Mr Nicky ti osserva incuriosito."
          = 2 THEN
            "Mr Nicky gironzola annusando."
          = 3 THEN
            "Mr Nicky poltrisce"
            IF CURRENT LOCATION IsA STANZA
              THEN "in un angolo"
              ELSE "a terra"
            END IF. "."
        END DEPEND.
    END IF.
END THE nicky.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
-- ALLESTIMENTO DELE SCENOGRAFIE 
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- Ora popoliamo le varie stanze e luoghi esterni con oggetti ed attori...

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- SCENOGRAFIA CUCINA 
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

--==============================================================================
-- LA SEDIA
--==============================================================================
-- Creiamo una sedia che sia utilizzabile.

THE sedia IsA supporto AT cucina.
  HAS articolo "la".
  
  VERB siediti_su
    DOES ONLY
      "[[ istanza 'sedia' --> VERB 'siediti_su' --> DOES ONLY ]]$n"
      IF hero IS sdraiato
        THEN "Ti alzi e"
          MAKE hero NOT sdraiato.
      END IF.
      "ti siedi" SAY superficie:prep_SU. SAY superficie. "."
      MAKE hero seduto.
  END VERB siediti_su.
END THE sedia.


--==============================================================================
-- IL FRIGORIFERO (contenitore_elencato + apribile + peso 50)
--==============================================================================

THE frigo IsA contenitore_elencato AT cucina.
  OPAQUE CONTAINER -- NOTE: The library doesn't initialize closed listed containers
                   --       to be automatically opaque!
  IS apribile.
  IS NOT aperto.
  HAS peso 50. -- Peso >= 50 impedisce i verbi 'prendi', 'prendi_da' e 'solleva'.
  

  INITIALIZE -- Consenti al frigo di contenere qualsiasi alimento del gioco:
    FOR EACH oAlimento IsA alimento
      DO INCLUDE oAlimento IN consentiti OF THIS.
    END FOR.
  
END THE frigo.

--==============================================================================
-- LA RADIO (dispositivo)
--==============================================================================
-- Facciamo in modo che si possa rompre la radio lanciandola, e poi ripararla.
THE radio IsA dispositivo IN tavolo_cucina.
  HAS articolo "la".
  HAS xDesc "Una piccola radio tascabile, a batterie.".
  
  VERB lancia
    DOES AFTER
      "$p[[ istanza 'radio' --> VERB 'lancia' --> DOES AFTER ]]$n"
      IF THIS IS acceso
        THEN "Dopo la caduta la radio si è spenta ... un brutto segno."
        ELSE "La radio sembra aver preso una bella botta, speriamo in bene."
      END IF.
      MAKE THIS rotto.
      MAKE THIS NOT acceso.
  END VERB lancia.

  VERB ripara
    DOES ONLY
      "[[ istanza 'radio' --> VERB 'ripara' --> DOES ONLY ]]
      $nSmanetti un po' con la radio finché torna a funzionare."
      MAKE THIS NOT rotto.
  END VERB ripara.
END THE radio.

--==============================================================================
-- IL TAVOLO DA CUCINA (supporto)
--==============================================================================

THE tavolo_cucina IsA supporto AT cucina.
  IS NOT prendibile.
  NAME tavolo.
END THE tavolo_cucina.

--==============================================================================
-- IL CESTO PER LA FRUTTA (contenitore_elencato sul tavolo)
--==============================================================================

THE cesto_frutta IsA cont_alimenti IN tavolo_cucina.
  NAME cesto.
  MENTIONED "cesto per la frutta"
END THE cesto_frutta.

--==============================================================================
-- Istanze alimenti: FRUTTA
--==============================================================================
-- Mettiamo un po' di frutta nel cesto per la frutta...

THE mela IsA frutto IN cesto_frutta.
  HAS articolo "la".
  NAME mela.
END THE.

THE banana IsA frutto IN cesto_frutta.
  HAS articolo "la".
  NAME banana.
END THE.

THE pera IsA frutto IN cesto_frutta.
  HAS articolo "la".
  NAME pera.
END THE.
--==============================================================================
-- Istanze bevande
--==============================================================================

--------------------------------------------------------------------------------
-- IL VASETTO VUOTO
--------------------------------------------------------------------------------
THE vasetto IsA cont_bevanda IN tavolo_cucina.
  NAME vasetto.
  HAS consentiti {vino}.
END THE.
--------------------------------------------------------------------------------
-- LA BOTTIGLIA DI VINO
--------------------------------------------------------------------------------
THE bottiglia IsA cont_bevanda IN tavolo_cucina.
  NAME bottiglia.
  HAS articolo "la".
END THE.

THE vino IsA liquido IN bottiglia.
  HAS vessel bottiglia. 
  INDEFINITE ARTICLE "del"
END THE.
--------------------------------------------------------------------------------
-- CONTENITORE DEL LATTE
--------------------------------------------------------------------------------
-- Un tetraedro ('Tetra Pak') di latte, opaco quando è chiuso, rivela i contenuti
-- quando è aperto.

THE tetraedro IsA cont_bevanda IN frigo.
  OPAQUE CONTAINER
  NAME 'cartone del latte'.
  NAME tetraedro.
  NAME cartone.
  IS apribile.
  IS NOT aperto.
  HAS xDesc
    "È un tetraedro per il latte.".
  
  -- Una volta aperto non può più essere richiuso:
  VERB chiudi
    DOES ONLY
      "[[ istanza 'tetraedro' --> VERB 'chiudi' --> DOES ONLY ]]
       $nUna volta aperto il cartone del latte non può più essere chiuso."
  END VERB chiudi.
END THE.

THE latte IsA liquido IN tetraedro.
  HAS vessel tetraedro. 
  INDEFINITE ARTICLE "del"
  HAS xDesc
    "Del latte intero, UHT.".
  
END THE.


--==============================================================================
-- Istanze snack
--==============================================================================
-- TODO: implemtalo come un contenitore mono-apribile (ossia, una volta aperto
--       il sacchetto è rotto e non lo si può richiudere).
THE sacchetto IsA cont_snack IN tavolo_cucina.
  OPAQUE CONTAINER.
  NAME sacchetto.
END THE sacchetto.

THE patatine IsA snack IN sacchetto.
  HAS articolo "le".
  INDEFINITE ARTICLE "delle"
END THE patatine.
-- =============================================================================

-- ARMI

-- =============================================================================
-- Qualche arma bianca e da fuoco per testare i verbi che le riguardano.

THE coltello IsA arma IN tavolo_cucina.
  HAS xDesc "Un grosso coltello, molto affilato.".
END THE coltello.

THE pistola IsA arma IN tavolo_cucina.
  NAME pistola.
  NAME rivoltella.
  HAS articolo "la".
  CAN sparare.
  HAS xDesc "È una rivoltella.".
END THE pistola.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- SCENOGRAFIA STUDIO
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

--==============================================================================
-- LA SCRIVANIA (supporto)
--==============================================================================

THE scrivania IsA supporto AT studio.
  HAS articolo "la".
  IS NOT prendibile.
  NAME tavolo.
END THE scrivania.

--==============================================================================

-- OGGETTI PER SCRIVERE E SCRIVIBILI

--==============================================================================
-- Alcuni oggetti scrivibili avranno una 'xDesc', altri no, così sarà possibile
-- testare i casi in cui 'esamina' si comporta come 'leggi'...

--==============================================================================
-- IL FOGLIO DI CARTA
--==============================================================================
THE foglio IsA ogg_scrivibile IN scrivania.
  NAME foglio.
  NAME carta.
  IS leggibile.
  IS scrivibile.
END THE foglio.

--==============================================================================
-- LA BUSTA DA LETTERA
--==============================================================================
THE busta_lettera IsA ogg_scrivibile IN scrivania.
  HAS articolo "la".
  NAME busta.
  NAME lettera.
  IS leggibile.
  IS scrivibile.
  HAS xDesc "È una busta da lettera, di buona qualità.".
END THE busta_lettera.

--==============================================================================
-- LA PENNA BIRO
--==============================================================================
THE penna IsA ogg_scrittura IN scrivania.
  HAS articolo "la".
  NAME penna.
  NAME biro.
  HAS xDesc "È una penna a sfera.".
END THE penna.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- SCENOGRAFIA GIARDINO
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================

--==============================================================================
-- LA STACCIONATA
--==============================================================================
-- La staccionata è un escamotage per poter mettere nel giardino cose e attori
-- non raggiungibili. L'idea è che anche se li si può vedere sono comunque non
-- raggiungibili (ma non distanti).

THE staccionata IsA object AT giardino.
  HAS articolo "la".
  DESCRIPTION
    "Oltre la staccionata intravedi un ombrellone e, in lontananza,
     la tua vicina."
END THE staccionata.

--==============================================================================
-- L'OMBRELLONE
--==============================================================================
-- È un oggetto non raggiungibile, utile per testare svariati verbi.

THE ombrellone IsA object AT giardino.
  NAME ombrellone.
  HAS articolo "l'".
  IS NOT raggiungibile.
END THE ombrellone.

--==============================================================================
-- L'AQUILONE
--==============================================================================
-- Un oggetto distante...
THE aquilone IsA object AT giardino.
  NAME aquilone.
  HAS articolo "l'".
  IS distante.
END THE aquilone.

--==============================================================================
-- IL PRATO (/erba)
--==============================================================================
-- Creiamo il prato solo perché ci serve un oggetto scenario da testare.

THE prato IsA object AT giardino.
  IS scenario.
  NAME prato.
  NAME erba.
END THE prato.

--==============================================================================
-- IL CINGUETTIO
--==============================================================================
-- Un 'suono' per testare sia 'ascolta' che con oggetti che NON sono:
--    esaminabile, prendibile, raggiungibile, spostabile.
THE cinguettio IsA suono AT giardino.
  DESCRIPTION "" -- non vogliamo che compaia nella descrizione (compare se si
                 -- 'ascolta' in giardino).

  VERB ascolta
    DOES ONLY
     "[[ istanza 'cinguettio' --> VERB 'ascolta' --> DOES ONLY ]]$n"
     IF hero AT ogg
        THEN "Si direbbe il canto della cinciallegra."
      ELSIF ogg NEAR hero
        THEN "Da questa distanza è difficile capire di che uccelli si tratti."
      END IF.
  END VERB ascolta.
END THE cinguettio.

--==============================================================================
-- IL TAVOLO DA GIARDINO (supporto)
--==============================================================================

THE tavolo_giardino IsA supporto AT giardino.
  IS NOT prendibile.
  NAME tavolo.
END THE tavolo_giardino.

--------------------------------------------------------------------------------
-- LA RIVISTA
--------------------------------------------------------------------------------
THE rivista IsA object IN tavolo_giardino.
  HAS articolo "la".
  -- @TODO: implementala come leggibile!
END THE rivista.


--==============================================================================
-- IL LETTINO
--==============================================================================
-- Creiamo un lettino su cui ci si possa sdraiare così da poter testare il verbo
-- 'sdraiati' ed i messaggi dei CHECK che bloccano le azioni intentate quando il
-- protagonista è sdraiato.

THE lettino IsA supporto AT giardino.
  NAME lettino.
  NAME letto.

  HAS xDesc "È un lettino pieghevole, di quelli usati in campeggio.".
  
  VERB sdraiati_su
    DOES ONLY
     "[[ istanza 'lettino' --> VERB 'sdraiati_su' --> DOES ONLY ]]$n"
      IF hero IS seduto
        THEN "Ti alzi e"
          MAKE hero NOT seduto.
      END IF.
      "ti sdrai" SAY superficie:prep_SU. SAY superficie. "."
      MAKE hero sdraiato.
  END VERB sdraiati_su.
END THE lettino.

--==============================================================================
-- LA PISCINA
--==============================================================================
-- Creiamo una piscina per testare sia 'nuota' che 'entra'.

THE piscina IsA liquido AT giardino.
  HAS articolo "la".
  NAME piscina.
  NAME acqua.

  VERB nuota
    DOES ONLY
      "[[ istanza 'piscina' --> VERB 'nuota' --> DOES ONLY ]]$n"
      IF hero DIRECTLY AT nella_piscina
        THEN "Sei già in piscina."
        ELSE
          LOCATE hero AT nella_piscina.
      END IF.
  END VERB nuota.
    
  VERB entra_in
    DOES ONLY
      "[[ istanza 'piscina' --> VERB 'entra_in' --> DOES ONLY ]]$n"
      IF hero DIRECTLY AT nella_piscina
        THEN "Sei già in piscina."
        ELSE
          LOCATE hero AT nella_piscina.
      END IF.
  END VERB entra_in.

  VERB esci_da
    DOES ONLY
      "[[ istanza 'piscina' --> VERB 'esci_da' --> DOES ONLY ]]$n"
      IF hero NOT DIRECTLY AT nella_piscina
        THEN "Non sei dentro la piscina."
        ELSE
          "Ti issi sul bordo della piscina ed esci."
          LOCATE hero AT giardino.
      END IF.
  END VERB esci_da.
END THE piscina.

--------------------------------------------------------------------------------
-- Dentro la piscina
--------------------------------------------------------------------------------
-- Per simulare quando il protagonista entra nella piscina creiamo un luogo
-- annidato nel giardino.

THE nella_piscina IsA location AT giardino.
  NAME 'GIARDINO DI CASA (DENTRO LA PISCINA)'.
  DESCRIPTION
    "La piscina è abbastanza ampia per fare delle piccole nuotate."

  ENTERED
    "[[ istanza 'nella_piscina' (location) --> ENTERED ]]$n"
    "Ti tuffi a bomba nella piscina."
END THE nella_piscina.

-- =============================================================================

-- ATTORI

-- =============================================================================
-- Qualche attore di base per testare i verbi...

--------------------------------------------------------------------------------
-- IL GIARDINIERE
--------------------------------------------------------------------------------
THE giardiniere IsA maschio AT giardino.
  HAS xDesc "Il giardiniere è intento a curare le piante.".
END THE giardiniere.

THE gatto IsA actor AT giardino.
END THE gatto.

--------------------------------------------------------------------------------
-- LA VICINA DI CASA
--------------------------------------------------------------------------------
-- La vicina è al di là della staccionata e in lontananza. Quindi non sarà
-- possibile parlarle, lanciarle cose, ecc.

THE vicina IsA femmina AT giardino.
  IS NOT raggiungibile.
  IS distante.
  DESCRIPTION "" -- La descriviamo assieme alla staccionata!
  HAS xDesc "È la tua vicina di casa, ma non sai come si chiama.".
END THE vicina.

--=============================================================================
--\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
--------------------------------------------------------------------------------
-- SCENOGRAFIA GUARDAROBA 
-------------------------------------------------------------------------------
--//////////////////////////////////////////////////////////////////////////////
--=============================================================================
-- Mettiamo a disposizione del nostro eroe un po' di vestiario con cui testare
-- i verbi per l'abbigliamento.


--==============================================================================
--------------------------------------------------------------------------------
-- MOBILIO
--------------------------------------------------------------------------------
--==============================================================================

--------------------------------------------------------------------------------
-- Cassettiera per la biancheria
--------------------------------------------------------------------------------
THE cassettiera IsA contenitore_elencato AT guardaroba.
  HAS articolo "la".
  HAS xDesc "La cassettiera in cui conservi la tua biancheria.".
  
END THE cassettiera.

--==============================================================================
--------------------------------------------------------------------------------
-- INDUMENTI 
--------------------------------------------------------------------------------
--==============================================================================
THE boxer IsA ind_mutande IN cassettiera.
  NAME boxer.
  HAS articolo "i".
END THE.

THE tanga IsA ind_mutande IN cassettiera.
  NAME tanga.
  HAS genere 2.
  HAS articolo "il".
  HAS xDesc "Un tanga rosso dimenticato qui da qualche tua ex.".
END THE.

THE reggiseno IsA ind_reggiseno IN cassettiera.
  HAS xDesc "Un reggiseno rosso dimenticato qui da qualche tua ex.".
END THE.

--------------------------------------------------------------------------------
START AT cucina.
DESCRIBE intestazione.

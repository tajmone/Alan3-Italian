--==============================================================================
-- "Test Articoli" by Tristano Ajmone, 2018.
--==============================================================================
-- NOTA: Questo esempio non affronta l'articolo per istanze di PERSON, MALE e
--       FEMALE perché non sono ancora implementate.
--------------------------------------------------------------------------------

IMPORT 'library.i'. -- ALAN Standard Library v2.1


THE my_game IsA DEFINITION_BLOCK

  HAS title    "Test Articoli".
  HAS subtitle "Prove tecniche dell'implementazione degli articoli italiani".
  HAS author   "Tristano Ajmone".
  HAS year     2018.
  HAS version  "1".

END THE.

--------------------------------------------------------------------------------

THE salotto IsA ROOM
  
END THE salotto.

--==============================================================================
-- ARTICOLI PREDEFINITI
--==============================================================================
-- La Libreria Italiana assegna dei valori predefiniti agli articoli determinati
-- ed indeterminati di tutte le cose (THING), in base al numero ed al genere:
-- 
-- +---+---+-----+-------+
-- | G | N | DET | INDET |
-- +---+---+-----+-------+
-- | M | S | il  | un    |
-- | M | P | i   | dei   |
-- | F | S | la  | una   |
-- | F | P | le  | delle |
-- +---+---+-----+-------+
-- 
-- Questi valori predefiniti sono adatti per molti sostantivi comuni, ma non
-- tutti; si tratta di una scelta arbitraria per semplificare la creazione di
-- classi ed istanze di cose. Nei rimanenti casi sarà necessario specificare
-- manualmente gli articoli idonei.

--------------------------------------------------------------------------------
-- Esempio M.S. -> "dado"                                     (un dado, il dado)
--------------------------------------------------------------------------------
THE dado IsA object AT salotto.
  -- se non altrimenti specificato, un oggetto è maschile singolare.
END THE dado.
--------------------------------------------------------------------------------
-- Esempio M.P. -> "confetti"                         (dei confetti, i confetti)
--------------------------------------------------------------------------------
THE confetti IsA object AT salotto.
  IS plural.
END THE confetti.
--------------------------------------------------------------------------------
-- Esempio F.S. -> "palla"                                 (una palla, la palla)
--------------------------------------------------------------------------------
THE palla IsA object AT salotto.
  IS femminile.
END THE palla.
--------------------------------------------------------------------------------
-- Esempio F.P. -> "caramelle"                   (delle caramelle, le caramelle)
--------------------------------------------------------------------------------
THE caramelle IsA object AT salotto.
  IS femminile.
  IS plural.
END THE caramelle.

--==============================================================================
-- ARTICOLI PERSONALIZZATI
--==============================================================================
-- Per assegnare ad un oggetto (classe o istanza) del gioco altre forme degli
-- articoli, sarà necessario specificarli manualmente, sovrascrivendo i valori
-- prederiniti della libreria, come illustrato qui di seguito.

--------------------------------------------------------------------------------
-- Esempio M.S. -> "zaino"                                 (uno zaino, lo zaino)
--------------------------------------------------------------------------------
THE zaino IsA object AT salotto.
  DEFINITE ARTICLE   "lo"
  INDEFINITE ARTICLE "uno"
END THE zaino.
--------------------------------------------------------------------------------
-- Esempio M.P. -> "spiccioli"                  (degli spiccioli, gli spiccioli)
--------------------------------------------------------------------------------
THE spiccioli IsA object AT salotto.
  IS plural.

  DEFINITE ARTICLE   "gli"
  INDEFINITE ARTICLE "degli"
END THE spiccioli.
--==============================================================================
-- ARTICOLI CON APOSTROFO
--==============================================================================
-- L'implementazione degli articoli con apostrofo richiede due accorgimenti:
-- (1) Dopo l'apostrofo va inserito il carattere speciale `$$`, grazie al quale
--     non verrà inserito uno spazio tra l'articolo e il sostantivo.
-- (2) Si dovrà aggiungere all'oggetto la forma completa <articolo + sostantivo>
--     come sinonimo, tramite NAME.
-- (3) Il punto (2) richiederà di dover anche dichiarare il nome di base dell'
--     oggetto tramite NAME (nel caso non lo si abbia già fatto), perché l'uso
--     di NAME prende il sopravvento sull'identificativo di base.
-- 
-- Il punto (2) è necessario poiché il parser dei comandi vedrà come un singolo
-- vocabolo l'articolo e il sostantivo, a causa della presenza dell'apostrofo.

--------------------------------------------------------------------------------
-- Esempio M.S. -> "arco"                                      (un arco, l'arco)
--------------------------------------------------------------------------------
THE arco IsA object AT salotto.
  NAME arco        --> È necessario ridefinire il nome di base.
  NAME 'l''arco'   --> Si noti l'escaping dell'apostrofo ('').

  DEFINITE ARTICLE   "l'$$"
  -- non è necessario ridefinire l'articolo indeterminato:
  -- useremo il valore predefinito ("un").
END THE arco.
--------------------------------------------------------------------------------
-- Esempio F.S. -> "arancia"                             (un'arancia, l'arancia)
--------------------------------------------------------------------------------
THE arancia IsA object AT salotto.
  IS femminile.

  NAME arancia
  NAME 'l''arancia'

  DEFINITE ARTICLE   "l'$$"
  INDEFINITE ARTICLE "un'$$"
END THE arancia.

--------------------------------------------------------------------------------
Start at salotto.
DESCRIBE banner.

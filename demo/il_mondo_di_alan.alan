-- Alan >= 3.0beta6 build 1866 | Alan Italian >= v0.13.0-Alpha
--==============================================================================
-- "Il Mondo di Alan" by Tristano Ajmone, 2018.
--  versione v0.11.0  (2019/01/17)
--==============================================================================
IMPORT 'libreria.i'. -- ALAN Libreria Standard Italiana v2.1

THE mia_AT IsA blocco_definizioni
  ------------------------------------------------------------------------------
  HAS       titolo  "Il Mondo di Alan".
  HAS  sottotitolo  "Una demo per testare ALAN Standar Library Italian".
  HAS       autore  "Tristano Ajmone".
  HAS         anno   2018.
  HAS     versione  "Alpha 1".
  HAS        AlanV  "v3.0beta6 build 1866".
  ------------------------------------------------------------------------------
END THE.



-- =============================================================================

----- INVENTARIO

-- =============================================================================
-- Qualche oggeto da dare all'eroe per testare i vari verbi...

--==============================================================================
-- Pistola
--==============================================================================
THE pistola IsA arma IN hero.
  HAS articolo "la".
  CAN sparare.
END THE pistola.

--==============================================================================
-- Coltello
--==============================================================================
THE coltello IsA arma IN hero.
  HAS articolo "il".
END THE coltello.



-- =============================================================================

----- MISCELLANEA

-- =============================================================================
-- Vari oggetti, comandi e altre cose non legate a nessun luogo specifico...

--==============================================================================
-- La Magia (non esaminabile)
--==============================================================================
-- Serve per testare vari verbi...

THE magia IsA OBJECT IN hero.
  HAS articolo "la".
  IS NOT esaminabile.
END THE magia.

--==============================================================================
-- INCANTESIMO XYZZY
--==============================================================================
-- In alcuni luoghi le parole magiche "xyzzy" possono sortire effetti speciali.
-- Ma qui ci limitiamo a definirne la sintassi ed il comportamento di base del
-- verbo.
SYNTAX xyzzy = xyzzy.

VERB xyzzy
  DOES
    "Pronunci la formula magica ""$$" STYLE EMPHASIZED. "xyzzy$$" STYLE NORMAL.
    """, ma non accade nulla di speciale."
END VERB xyzzy.
-- =============================================================================

----- Piazza della Repubblica d'IF

-- =============================================================================
THE pzzaIF IsA luogo_esterno
  NAME 'Piazza della Repubblica d''IF'.
  EXIT est   TO Eden.
  EXIT ovest TO mercato.
  EXIT nord  TO ccAtrio.
  DESCRIPTION "Ti trovi in una piazza spaziosa.
               Odi delle voci in lontananza." ---> le 'voci' al mercato!
END THE pzzaIF.

THE statua_Alan IsA object AT pzzaIF.
  HAS articolo "la".
  NAME statua.
  NAME piedistallo.
  IS NOT spostabile.
  MENTIONED  --| Facciamo sì che una volta letta la targhetta la statua venga
    "statua" --| menzionata in maniera più specifica...
    IF targhetta IS già_letta
      THEN "di Alan III"
    END IF.
  HAS xDesc
    "La statua in bronzo raffigura un uomo elegante che sta entrando in un vecchio
     monitor CRT. La gamba destra è già dentro lo schermo, fino al ginocchio.
     Nella mano sinistra impugna una valigia da viaggio, mentre la destra poggia
     sul cappello, come se volesse impedirgli di volare via. L'impressione è che
     l'uomo stia partendo per un'avventura che inizia al di là del monitor.
     $pSul piedistallo in pietra noti una targhetta su cui è inciso del testo.".

END THE statua_Alan.

THE targhetta IsA object AT pzzaIF.
  HAS articolo "la".
  DESCRIPTION "" ---> non elencare tra le cose visibili.
  IS leggibile.
  HAS testo "Alan III".
  IS NOT già_letta.
  VERB prendi --| Creiamo una risposta personalizzata per quando il giocatore
    DOES ONLY --| tenta di prendera la targhetta...
      "La targhetta è saldamente ancorata al piedistallo."
  END VERB prendi.
  VERB esamina, leggi --| Tracciamo se la targhetta è già stata letta, tramite un
    DOES AFTER        --| attributo booleano. Ora il giocatore ne conosce il nome.
      MAKE THIS già_letta.
  END VERB esamina.
END THE targhetta.

--==============================================================================
-- Scatole e Scatoloni...
--==============================================================================
-- Per testare risposte contenitori già aperti/chiusi

THE scatola IsA contenitore_elencato AT pzzaIF.
  OPAQUE CONTAINER
  HAS articolo "la".
  IS apribile.
  IS NOT aperto.  
END THE scatola.

THE scatoline IsA contenitore_elencato AT pzzaIF.
  OPAQUE CONTAINER
  HAS articolo "le".
  IS apribile.
  IS NOT aperto.  
END THE scatoline.

THE scatolone IsA contenitore_elencato AT pzzaIF.
  OPAQUE CONTAINER
  HAS articolo "lo".
  IS apribile.
  IS NOT aperto.  
END THE scatolone.

THE contenitori IsA contenitore_elencato AT pzzaIF.
  OPAQUE CONTAINER
  HAS articolo "i".
  IS apribile.
  IS NOT aperto.  
END THE contenitori.


--==============================================================================
-- Corvo Distante
--==============================================================================
-- Mi serve per testare le risposte dei verbi con oggetti 'distanti'...
THE corvo IsA OBJECT AT pzzaIF.
  IS distante.

END THE corvo.


--==============================================================================
-- La Pozzanghera
--==============================================================================
-- TODO: Va sostituita con un esempio migliore, che abbia un senso.
--       Per ora la lascio qui, al fine di poter testare i vari verbi, ma dovrò
--       integrarla meglio nell'avventura.
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- Questa ci serve per testare il verbo bere con liquidi non contenuti (se il
-- liquido non è in un contenitore, allora viene bevuto solo in parte e non
-- consumato).

THE pozzanghera IsA liquido AT pzzaIF.
  HAS articolo "la".
  IS  potabile.
END THE pozzanghera.

--==============================================================================
-- Penna
--==============================================================================
-- Per poter scrivere sul foglio.

THE penna IsA OBJECT AT pzzaIF.
  HAS articolo "la".
END THE penna.

--==============================================================================
-- Foglio di carta
--==============================================================================
-- Per testare verbi LEGGI e SCRIVI.

THE foglio_bianco IsA OBJECT AT pzzaIF.
  NAME foglio.
  NAME foglio bianco.
  MENTIONED
    "foglio di carta"
    IF testo OF THIS = ""
      THEN "bianco"
    END IF.
  IS leggibile.
  IS scrivibile.

  VERB scrivi

    DOES ONLY
      IF penna IN hero
        THEN
          IF testo <> "" ---> Non si accettano stringhe vuote!
            THEN
              IF testo OF ogg = ""
                THEN SET testo OF ogg TO testo.
                ELSE SET testo OF ogg TO testo OF ogg + " " + testo.
              END IF.
              "Scrivi ""$$" SAY testo. "$$""" SAY ogg:prep_SU. SAY ogg. "."
              MAKE ogg leggibile.
            ELSE "Devi fornire dei contenuti da scrivere!"
          END IF.
      ELSE
          "Non hai nulla con cui scrivere."
      END IF.
  END VERB.

END THE foglio_bianco.

--==============================================================================
-- Libro Magico
--==============================================================================
THE libro_magico IsA OBJECT AT pzzaIF.
  NAME libro.
  NAME libro magico.
  NAME libro antico.
  MENTIONED "libro antico"
  HAS xDesc
    "Sembra un libro molto antico. La pelle in cui è rilegato è imbrunita dai 
     secoli e fatichi a decodificarne i simboli arcaici che vi sono incisi.".
  IS leggibile.
  HAS testo
    "La dottrina magica degli antichi fu edficata su un'unica formula sacra che
     ci è stata tramandata di generazione in generazione. Quella formula è 
     ""XYZZY"".".
END THE libro_magico.


--==============================================================================
-- Oggetto Senza Descrizione
--==============================================================================
-- Per testare la risposta di esamina quando 'ex' non è definito.
THE sasso IsA OBJECT AT pzzaIF.
  
END THE sasso.

--==============================================================================
-- Latte in Bottiglia
--==============================================================================
-- Per testare bevi

THE bottiglia IsA contenitore_elencato AT pzzaIF.
  HAS articolo "la".
END THE bottiglia.

THE latte IsA liquido IN bottiglia.
  HAS articolo "il".
  INDEFINITE ARTICLE "del"
  HAS recipiente bottiglia.
  IS  potabile.
END THE latte.

THE aranciata IsA liquido IN bottiglia.
  HAS articolo "l'".
  INDEFINITE ARTICLE "dell'$$"
  IS femminile.
  HAS recipiente bottiglia.
  IS  potabile.
END THE aranciata.

--==============================================================================
-- Radio Rotta
--==============================================================================
-- Per testare attributo 'rotto'

THE radio IsA dispositivo AT pzzaIF.
  HAS articolo "la".
  IS rotto.
END THE radio.

-- =============================================================================

----- Giardino dell'Eden

-- =============================================================================
THE Eden IsA luogo_esterno.
  NAME 'Giardino dell''Eden'.
  EXIT ovest TO pzzaIF.
END THE Eden.

THE albero IsA object AT Eden.
  HAS articolo "l'".
  NAME albero.
  IS NOT spostabile.
END THE albero.

--==============================================================================
-- Le Gemelle
--==============================================================================
-- Per testare verbi con attori al plurale...

THE gemelle IsA femmina AT Eden.
  HAS articolo "le".
END THE gemelle.


--==============================================================================
-- La Mela
--==============================================================================
-- TODO: Devo fare in modo che venga descritta meglio, che sia chiaro che si
--       trova sull'albero e che il giocatore dovrà arrampicarvisi per poterla
--       prendere.
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- La mela per ora serve solo per testare un oggetto fuori portata.
THE mela IsA frutto AT Eden.
  HAS articolo "la".
  NAME mela.
  IS NOT raggiungibile.
END THE mela.

--==============================================================================
-- Lo Scrigno
--==============================================================================
-- Per testare contenitori con chiavi...

THE scrigno IsA contenitore_elencato AT Eden.
  OPAQUE CONTAINER
  -- IS OPAQUE.
  HAS articolo "lo".
  IS apribile.
  IS NOT aperto.
  IS bloccabile.
  IS bloccato.
  HAS chiave_abbinata chiave_scrigno.
END THE scrigno.


-- ==============
-- Chiave Scrigno
-- ==============
THE chiave_scrigno IsA object AT Eden.
  HAS articolo "la".
  NAME chiave.

END THE chiave_scrigno.


-- ==============
-- lingotto d'Oro
-- ==============
THE lingotto IsA object IN scrigno.
  MENTIONED "lingotto d'oro"
END THE lingotto.

-- Per testare 'empty' and 'pour'
THE baule IsA contenitore_elencato AT Eden.
  OPAQUE CONTAINER
  HAS articolo "il".
  IS apribile.
  IS NOT aperto.
  HAS consentiti { scrigno }.
END THE baule.


-- =============================================================================

----- Mercato

-- =============================================================================
-- Il mercato è il posto ideale per testare i supporti (la bancarella), i verbi
-- e messaggi per PRENDI, LASCIA e MANGIA, e l'uso di TUTTO e TRANNE.
--------------------------------------------------------------------------------
THE mercato IsA luogo_esterno.
  EXIT est TO pzzaIF.

  VERB xyzzy  --| INCANTESIMO "XYZZY" -- trasferisce magicamente tutta la frutta
    DOES ONLY --| nell'inventario (utile per accellerare i vari test).
      "Pronunci la formula magica ""$$" STYLE EMPHASIZED. "xyzzy$$" STYLE NORMAL.
      """ e tutta la frutta finisce nel tuo inventario!"
      FOR EACH fr IsA frutto, AT CURRENT LOCATION DO
        IF fr NOT IN hero
          THEN LOCATE fr IN hero.
        END IF.
      END FOR.
  END VERB xyzzy.
END THE mercato.


--==============================================================================
-- Voci di Sottofondo
--==============================================================================
-- TODO: Anche se è solo un test, andrebbe ripulito e reso più interessante!
--       Potrei cambiarle in urla di una lite, e fare in modo che quando il
--       giocatore arriva al mercato assiste alla lite e poi le urla cessano
--       (le sposto nel NOWHERE). Così le trasformo in un pretesto per attirare
--       il giocatore al mercato, e la lite può servire ai fini della storia.
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- Creiamo un rumore di sottofondo, che può essere ascoltato anche da luoghi
-- adiacenti (il verbo 'ascolta' può riferirsi a oggetti in altre locazioni).
-- Ovviamente, dovremmo menzionare il suono manualmente nelle altre stanze (con
-- DESCRIPTION) al fine di attirare l'attenzione su di esso, dato che si trova
-- solo nel mercato in realtà.
-- Utile soprattutto per testare i messaggi di errore dei verbi che esigono che
-- l'oggetto sia esaminabile (i suoni non lo sono).
--------------------------------------------------------------------------------
THE voci IsA suono AT mercato.
  HAS articolo "le".
  DESCRIPTION "" ---> non elencare

  VERB ascolta
    DOES ONLY
      IF THIS AT hero
        THEN "Stanno discutendo sui prezzi della frutta."
      ELSIF THIS NEAR hero
        THEN "Le voci sembrano venire dal mercato."
      ELSE   "Non senti alcune voci qui."
      END IF.

  END VERB.
END THE voci.


THE Gustavo IsA maschio AT mercato.
  HAS nome_proprio.
END THE Gustavo.

THE fruttivendolo IsA maschio AT mercato.
END THE fruttivendolo.

--==============================================================================
-- Bancarella della frutta
--==============================================================================
THE bancarella IsA supporto AT mercato.
  HAS articolo "la".
END THE bancarella.

EVERY frutto IsA object.
  IS commestibile.
  NAME frutto.
END EVERY frutto.

THE pera IsA frutto IN bancarella.
  HAS articolo "la".
  NAME pera.
END THE pera.

THE mango IsA frutto IN bancarella.
  NAME mango.
END THE mango.

THE cigliege IsA frutto IN bancarella.
  HAS articolo "le".
  NAME cigliege.
  -- INDEFINITE ARTICLE "alcune" -- anziché "delle"
END THE cigliege.
-- =============================================================================

----- Centro Commerciale

-- =============================================================================
-- Per praticità, tutte le stanze del Centro Commerciale avranno nel loro ID il
-- suffisso "cc".

-- =============================================================================

----- Atrio

-- =============================================================================
THE ccAtrio IsA stanza.
  NAME 'Atrio del Centro Commerciale'
  DESCRIPTION "Sei circondato da negozi."
  EXIT sud   TO pzzaIF.
  EXIT ovest TO ccEmporio.
END THE ccAtrio.


--==============================================================================
-- Candela
--==============================================================================
-- Per testare 'fonte_di_luce' naturale

THE candela IsA fonte_di_luce AT ccAtrio.
  HAS articolo "la".
  -- IS naturale. ---> Attributo `naturale` eliminato dalla libreria!
  -- VERB esamina
  --   DOES ONLY
  --   IF THIS IS illuminato
  --     THEN "La candela arde illuminando l'ambiente, la cera fusa forma delle
  --           gocce lungo il suo fusto."
  --     ELSE "La candela è spenta."
  --   END IF.
  -- END VERB.
END THE candela.

--==============================================================================
-- Lampadine
--==============================================================================
-- Per testare 'fonte_di_luce' NOT naturale

THE lampadine IsA fonte_di_luce AT ccAtrio.
  HAS articolo "le".
  IS NOT naturale.
  -- VERB esamina
  --   DOES ONLY
  --   IF THIS IS illuminato
  --     THEN "Le lampadine emettono un'intensa luce fredda."
  --     ELSE "Le lampdadine sono spente."
  --   END IF.
  -- END VERB.
END THE lampadine.

--==============================================================================
-- Bollitore
--==============================================================================
-- Per testare 'fonte_di_luce' naturale

THE bollitore IsA dispositivo AT ccAtrio.
  HAS articolo "il".
  HAS xDesc "È un vecchio bollitore elettrico.".
  -- VERB esamina
  --   DOES ONLY
  --   IF THIS IS acceso
  --     THEN "Dal bollitore esce una nube di vapore caldo."
  --     ELSE "Il bollitore è in immobile attesa di essere acceso."
  --   END IF.
  -- END VERB.
END THE bollitore.


-- =============================================================================

----- Emporio Giorgio Alani

-- =============================================================================
-- L'Emporio Alani è il posto giusto per testare tutto quello che concerne il
-- vestiario.
THE ccEmporio IsA stanza.
  NAME 'Emporio Alani'.
  EXIT est to ccAtrio.
END THE ccEmporio.


THE commessa IsA femmina AT ccEmporio.
  -- HAS articolo "la".

END THE commessa.



--------------------------------------------------------------------------------
Start at pzzaIF.
DESCRIBE intestazione.
